EventPointerTable(0xD, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
TURN 0x0 CutsceneMocha [1,0] 0x0
TURN 0x0 OhHeckItHim [2,0] 0x80
TURN 0x0 BridgeRein [3,0] 0x0
TURN 0x0 BoatRein [5,0] 0x0
TURN 0x0 MountainRein [6,0] 0x0
TURN 0x0 EastRein [7,0] 0x0
TURN 0x0 NorthRein [8,0] 0x0
TURN 0x0 GoodbyeDespair [10,0] 0x0
	END_MAIN

ALIGN 32
CharacterBasedEvents:
CharacterEventBothWays(0x21, PandoroRecruit, Ulrich, Pandoro)
CharacterEventBothWays(0x21, PandoroAffoRecruit, Affogato, Pandoro)
CharacterEventBothWays(0x21, BreneRecruit, Affogato, Irene)
CharacterEventBothWays(0x21, BreneBreveRecruit, Breve, Irene)
CharacterEventBothWays(0x22, MochaNoraConvo, Nora, Mocha)
CharacterEventBothWays(0x23, HildrBreveConvo, Breve, Hildr)
CharacterEventBothWays(0x24, AffoSeqConvo, Affogato, Sequin)
	END_MAIN

MiscBasedEvents:
	CauseGameOverIfLordDies
AFEV 0x0 RunRunRunRun 0x18
AFEV 0x0 EndingScene 0x3
	END_MAIN

LocationBasedEvents:
Chest(Slow,1,9)
Arrive(0,1)
Arrive(0,2)
Village(0x20,SallyCars,13,3)	
House(0x0,OgrimHouseEpic,9,8)
	END_MAIN

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
LoadUnit(7,16)
LoadUnit(8,16)
LoadUnit(10,16)
LoadUnit(11,16)
LoadUnit(9,16)
LoadUnit(8,17)
LoadUnit(10,17)
	UNIT
	
CutscenePeg:
UNIT Mocha PegasusRider 0x1 Level(3, Ally, 0)     [14,0] 0 0 1 REDA9R2 [SteelBlade,SteelBlade,SteelBlade,SteelBlade,] AttackInRangeAI
	UNIT
	
WoohooPeg:
UNIT Mocha PegasusRider 0x1 Level(3, Ally, 0)     [5,18] 0 0 1 REDA5R15 [IronLance,Torch] AttackInRangeAI
	UNIT
	
LunaPirate:
UNIT Pandoro Pirate 0x1 Level(4, Ally, 0)   [1,11] 0b 0x0 0x0 0x0 [IronLabrys,PureWater] BossAI
UNIT Irene Myrmidon_F 0x1 Level(3, Ally, 0)   [0,10] 0b 0x0 0x0 0x0 [KillingEdge,Vulnerary] BossAI
	UNIT

OhGodOhFuck:
UNIT 0x69 Parson 0x1 Level(1, Enemy, 0)   [9,8] DropItem 0 1 REDA9R9 [Light2,Boots] [0x6,0x3,0x4,0x20]
	UNIT

EnemyUnits:
UNIT AeteriaStealable Fighter 0x1 Level(2, Enemy, 1)     [10,11] 0x0 0x0 0x0 0x0 [IronAxe,Vulnerary] TwoActionAI
UNIT AeteriaStealable AArmor 0x1 Level(2, Enemy, 1)     [9,4] 0x0 0x0 0x0 0x0 [IronLabrys,EnergyRing] BossAI
UNIT Aeteria Soldier 0x1 Level(3, Enemy, 1)     [3,10] 0x0 0x0 0x0 0x0 [IronLance] BossAI
UNIT Aeteria Mercenary 0x1 Level(2, Enemy, 1)     [7,9] 0x0 0x0 0x0 0x0 [IronSword] AttackInRangeAI
UNIT Aeteria Soldier 0x1 Level(2, Enemy, 1)     [7,13] 0x0 0x0 0x0 0x0 [IronLance,] ChargeAI
UNIT Aeteria Mage 0x1 Level(2, Enemy, 1)     [11,14] 0x0 0x0 0x0 0x0 [Fire,] ChargeAI
UNIT Aeteria Shaman 0x1 Level(3, Enemy, 1)     [5,11] 0x0 0x0 0x0 0x0 [Flux,] AttackInRangeAI
UNIT Aeteria Myrmidon 0x1 Level(3, Enemy, 1)     [13,5] 0x0 0x0 0x0 0x0 [Sagitar,] AttackInRangeAI
UNIT Aeteria PegRider 0x1 Level(2, Enemy, 1)     [9,0] 0x0 0x0 0x0 0x0 [IronLance,] StallOneTurnAI
UNIT Aeteria PegRider 0x1 Level(2, Enemy, 1)     [13,1] 0x0 0x0 0x0 0x0 [IronLance,] AttackInRangeAI
UNIT Aeteria Shaman 0x1 Level(2, Enemy, 1)     [11,8] 0x0 0x0 0x0 0x0 [DarkTome2,] BossAI
UNIT Aeteria Soldier 0x1 Level(2, Enemy, 1)     [13,10] 0x0 0x0 0x0 0x0 [SteelLance,] AttackInRangeAI
UNIT Aeteria ACav 0x1 Level(2, Enemy, 1)     [5,3] 0x0 0x0 0x0 0x0 [IronAxe,] TwoActionAI
UNIT Aeteria LCav 0x1 Level(3, Enemy, 1)     [5,0] 0x0 0x0 0x0 0x0 [IronLance,] StallOneTurnAI
UNIT Aeteria Thaumaturge 0x1 Level(2, Enemy, 0)     [14,8] DropItem 0x0 0x0 0x0 [Fire,RedGem] StallOneTurnAI
UNIT Aeteria SCav 0x1 Level(2, Enemy, 1)     [0,2] 0x0 0x0 0x0 0x0 [SteelSword,] StallOneTurnAI
	UNIT
	
TurnOnelalala:
UNIT Aeteria Raider 0x1 Level(2, Enemy, 1)     [0,1] 0x0 0x0 0x0 0x0 [IronSword,] BrigandAI
	UNIT
	
BridgeEnemies:
UNIT Aeteria ACav 0x1 Level(2, Enemy, 1)     [0,2] 0b 0x0 0x0 0x0 [SteelAxe,] StallOneTurnAI
UNIT Aeteria SCav 0x1 Level(3, Enemy, 1)     [0,1] 0b 0x0 0x0 0x0 [IronSword,] StallOneTurnAI
UNIT

BoatEnemies:
UNIT UPirate Pirate 0x1 Level(3, Enemy, 1)     [1,14] 0b 0x0 0x0 0x0 [IronAxe,] TwoActionAI
UNIT UPirate Pirate 0x1 Level(3, Enemy, 1)     [3,18] 0b 0x0 0x0 0x0 [IronAxe,] TwoActionAI
UNIT

MountainEnemies:
UNIT Aeteria PegRider 0x1 Level(2, Enemy, 1)     [9,0] 0b 0x0 0x0 0x0 [IronLance] StallOneTurnAI
UNIT Aeteria PegRider 0x1 Level(3, Enemy, 1)     [12,0] 0b 0x0 0x0 0x0 [IronLance] StallOneTurnAI
UNIT

EastEnemies:
UNIT UPirate Pirate 0x1 Level(4, Enemy, 1)     [0,6] 0b 0x0 0x0 0x0 [SteelAxe] ChargeAI
UNIT Aeteria Occultist 0x1 Level(3, Enemy, 1)     [0,5] 0b 0x0 0x0 0x0 [DarkTome2] StallOneTurnAI
UNIT

SouthEnemies:
UNIT Aeteria PegRider 0x1 Level(3, Enemy, 1)     [14,11] 0b 0x0 0x0 0x0 [Horseslayer] ChargeAI
UNIT Aeteria PegRider 0x1 Level(3, Enemy, 1)     [14,12] 0b 0x0 0x0 0x0 [SteelLance] ChargeAI
UNIT

NorthEnemies:
UNIT Aeteria Shaman 0x1 Level(4, Enemy, 1)     [14,5] 0b 0x0 0x0 0x0 [DarkTome2] TwoActionAI
UNIT Aeteria Occultist 0x1 Level(3, Enemy, 1)     [14,4] 0b 0x0 0x0 0x0 [DarkTome1] ChargeAI
UNIT

Traps1:
BlueSigil(0,1)
BlueSigil(0,2)
TelliusTorchOn(8,9,3)
TelliusTorchOn(0,9,3)
TelliusTorchOn(9,14,3)
TelliusTorchOn(7,0,3)
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
SetBackground(0x35)
Music(0xA9)
FADU 15
Text(Ch2Opening1)
SetBackground(0x2B)
SOUN 0xA6
STAL 3
SOUN 0xA7
STAL 5
SOUN 0xA6
STAL 3
SOUN 0xA7
STAL 5
SOUN 0xA6
STAL 3
SOUN 0xA7
STAL 5
MUSCSLOW 0x24
CharacterConditionalDialog(Evelyn,Ch2Opening2,Ch2OpeningAlt2)
FADI 60
	LOAD1 0x1 EnemyUnits
	LOAD1 0x1 LunaPirate
	ENUN
	CUSE Pandoro
	CUSE Irene
CLEAN
FADU 60
STAL 10
CameraCursor(Irene)
STAL 3
CameraCursor(Pandoro)
STAL 15
MUSC 0x4B
Text(Ch2Opening3)
STAL 10
FADI 60
SetBackground(0x3F)
Text(Ch2Opening4)
GotoPrepScreen
	ENDA

EndingScene:
MUSI
Text(Ch2Ending1)
MUNO
MusicChange(VictoryBGM)
STAL 15
SetBackground(0x3B)
DoubleIfAliveDialog(Irene,Pandoro,Ch2Ending2,1)
	MNC2 0x3
	NoFade
	ENDA
	
CutsceneMocha:
CAM1 [10,2]
CamOn
	LOAD2 0x1 CutscenePeg
ENUN
CUMO [9,2]
STAL 60
CURE
Text(Ch2MochaAppear1)
STAL 30
MOVE 0x3C Mocha [14,0]
ENUN
DISA Mocha
STAL 5
CAM1 [2,1]
	LOAD1 0x1 WoohooPeg
CameraCursor(Mocha)
MUSS 0x30
Text(Ch2MochaAppear2)
CamOff
	NoFade
ENDA

PandoroRecruit:
MUSS 0x30
STAL 33
Text(PanUlrichRecruit)
MURE 0x2
CUSA Pandoro
CUSA Irene
NoFade
ENDA

PandoroAffoRecruit:
MUSS 0x30
STAL 33
Text(PanAffoRecruit)
MURE 0x2
CUSA Pandoro
CUSA Irene
NoFade
ENDA

BreneRecruit:
MUSS 0x30
STAL 33
Text(IreneAffoRecruit)
MURE 0x2
CUSA Pandoro
CUSA Irene
NoFade
ENDA

BreneBreveRecruit:
MUSS 0x30
STAL 33
Text(IreneBreveRecruit)
MURE 0x2
CUSA Pandoro
CUSA Irene
NoFade
ENDA

MochaNoraConvo:
	MUSI
	Text(MochaEvelTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
HildrBreveConvo:
	MUSI
	Text(HildrBreveTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
AffoSeqConvo:
	MUSI
	Text(AffoSeqTalk)
	MUNO
	MURE 2
	NoFade
	ENDA

BridgeRein:
SpawnReinforcements(BridgeEnemies)
NoFade
ENDA

BoatRein:
SpawnReinforcements(BoatEnemies)
NoFade
ENDA

MountainRein:
SpawnReinforcements(MountainEnemies)
NoFade
ENDA

EastRein:
SpawnReinforcements(EastEnemies)
STAL 20
SpawnReinforcements(SouthEnemies)
NoFade
ENDA

NorthRein:
SpawnReinforcements(NorthEnemies)
NoFade
ENDA

SallyCars:
VillageEventItem(SwordreaverHouseText, HouseBG1, Swordreaver)
ENDA

OgrimHouseEpic:
MUSI // lowers music volume
CHECK_ALIVE 0x69
BEQ 1 0x0 0xC
CameraCursor(0x69)
Text(OgrimHouseEnter)

TEXTSTART
CHECK_ACTIVEID Affogato
BNE 0x0 0xC 0x0
	SVAL 0x2 0x1
	SVAL 0x3 OgrimHouse
	CALL 0x9EE310
	REMA
	GOTO 0x1
LABEL 0x0
SVAL 0x2 0x1
SVAL 0x3 OgrimAffoHouse
CALL 0x9EE310
REMA
LABEL 0x1
CHECK_EVENTID 0xB
BNE 0x776 0xC 0x0
GiveExperience(0xFFFF,15)
ENUT 0xB
LABEL 0x776
EVBIT_T 0x7
MUNO // increases music volume

LABEL 1
NoFade
ENDA

OhHeckItHim:
LOAD1 0x1 TurnOnelalala
STAL 5
CAM1[7,8]
Text(HouseBG2, Ch2OgrimAppear1)
LOAD1 0x1 OhGodOhFuck
CameraCursor(0x69)
	TEXTSTART
	Text(Ch2OgrimAppear2)
	TEXTEND
ENUT 0x18
NoFade
ENDA

RunRunRunRun:
CHECK_EVENTID 0x19
BNE 0x776 0x0 0xC
CHECK_EVENTID 0x10
BEQ 0x776 0xC 0x0 //If Flag 0x10 is on, continue. Otherwise, skip to 0x776
	SVAL 0x1 0x00040000
	CHAI 0x69
CHECK_ALIVE 0x69
BEQ 0x776 0x0 0xC
CAM1 0x69
STAL 20
FlashCursor(0x69,60)
Text(OgrimChargeText)
ENUT 0x19
LABEL 0x776
NoFade
ENDA

GoodbyeDespair:
GameOver
ENDA

