EventPointerTable(Ch5Event, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
TURN 0x0 Turn1WTF [1,0] 0x0
TURN 0x0 LMoatEvent1 [2,0] 0x0
TURN 0x0 TMoatEvent1 [3,0] 0x0
TURN 0x0 LRFortEvent1 [4,0] 0x0
TURN 0x0 MFortEvent2 [4,0] 0x0
TURN 0x0 TMoatEvent2 [5,0] 0x0
TURN 0x0 LFortEvent1 [6,0] 0x0
TURN 0x0 RFortEvent3 [6,0] 0x0
TURN 0x0 LMoatEvent2 [4,0] 0x0
TURN 0x0 TMoatEvent3 [7,0] 0x0
TURN 0x0 MidOneEvent1 [8,0] 0x0
TURN 0x0 RFortEvent2 [9,0] 0x0
TURN 0x0 LMoatEvent3 [10,0] 0x0
TURN 0x0 MFortEvent1 [11,0] 0x0
TURN 0x0 TriggerHappyHavoc [13,0] 0x0
	END_MAIN

ALIGN 32
CharacterBasedEvents:
CharacterEvent(0x0,YoryTurretCheck,Yory,0x3F)
CharacterEventBothWays(0x10,AffoGorloisConvo, Affo, Gorlois)
CharacterEventBothWays(0x11,IronRuneConvo, Gorlois, Stroganoff)
CharacterEventBothWays(0x12,AnsgarThereseConvo, Ansgar, Therese)
CharacterEventBothWays(0x13,AffoKingConvo, Affo, King)
CharacterEventBothWays(0x14,NoraKingConvo, Nora, King)
CharacterEventBothWays(0x15,StroganSeqConvo, Stroganoff, Sequin)
CharacterEventBothWays(0x16,BrudetMochaConvo, Brudet, Mocha)
CharacterEventBothWays(0x17,BreveFlorisConvo, Breve, Floris)
CharacterEventBothWays(0x18,RinaHildrConvo, Rina, Hildr)
CharacterEventBothWays(0x19,LennardPandoroConvo, Lennard, Pandoro)
CharacterEventBothWays(0x20,EisNoelConvo, Eiskaffee, Noel)
CharacterEventBothWays(0x21,DoppioBrudetConvo, Doppio, Brudet)
CharacterEventBothWays(0x22,StroganBrudetConvo, Stroganoff, Brudet)
CharacterEventBothWays(0x23,YoryLiliConvo, Yory, Lili)
	END_MAIN

ALIGN 32
MiscBasedEvents:
AREA 0xD KillTopMoat [17,2] [18,2]
AREA 0xA FortniteLeft [5,18] [5,18]
AREA 0xB FortniteMid [13,11] [13,11]
AREA 0xC FortniteRight [18,16] [18,16]
	CauseGameOverIfLordDies
	END_MAIN

LocationBasedEvents:
Chest(MasterSeal,21,0)
Chest(0x74,12,6)
Chest(0x5F,14,7)
Door(10,9)
Door(21,3)
Door(15,8)
Door(17,8)
Door(18,8)
	END_MAIN

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
LoadUnit(5,4)
LoadUnit(6,4)
LoadUnit(7,4)
LoadUnit(5,5)
LoadUnit(6,5)
LoadUnit(7,5)
LoadUnit(8,4)
LoadUnit(8,5)
LoadUnit(18,5)
LoadUnit(19,5)
LoadUnit(20,5)
LoadUnit(18,6)
LoadUnit(19,6)
LoadUnit(20,6)
LoadUnit(21,5)
LoadUnit(21,6)
	UNIT

EnemyUnits:
UNIT Aeteria Occultist 0x1 Level(7, Enemy, 1)     [0,9] DropItem 0x0 0x0 0x0 [Flux,Elixir] ChargeAI
UNIT Aeteria WyvernRider 0x1 Level(6, Enemy, 1)     [1,9] 0b 0x0 0x0 0x0 [IronLabrys] ChargeAI
UNIT Aeteria AxeArmor 0x1 Level(7, Enemy, 1)     [9,9] DropItem 0x0 0x0 0x0 [SteelLabrys,ShortAxe,DoorKey] BossAI
UNIT Aeteria Shaman 0x1 Level(6, Enemy, 1)     [1,13] 0b 0x0 0x0 0x0 [DarkTome2] AttackInRangeAI
UNIT Aeteria Thaumaturge 0x1 Level(6, Enemy, 1)     [6,11] 0b 0x0 0x0 0x0 [Thunder] StallOneTurnAI
UNIT Aeteria Myrmidon 0x1 Level(6, Enemy, 1)     [20,11] 0b 0x0 0x0 0x0 [KillingEdge] TwoActionAI
UNIT Aeteria Occultist 0x1 Level(6, Enemy, 1)     [22,10] DropItem 0x0 0x0 0x0 [Flux,Mine] ChargeAI
UNIT Aeteria LanceArmor 0x1 Level(7, Enemy, 1)     [17,7] 0b 0x0 0x0 0x0 [Spear] BossAI
UNIT Aeteria Occultist 0x1 Level(6, Enemy, 1)     [0,5] 0b 0x0 0x0 0x0 [DarkTome2] StallOneTurnAI
UNIT AeteriaStealable Soldier 0x1 Level(6, Enemy, 1)     [5,7] 0b 0x0 0x0 0x0 [IronGlaive,Elixir] ChargeAI
UNIT Aeteria Mercenary 0x1 Level(6, Enemy, 1)     [3,12] 0b 0x0 0x0 0x0 [IronSword,Zanbato] AttackInRangeAI
UNIT Aeteria Archer 0x1 Level(6, Enemy, 1)     [13,2] 0b 0x0 0x0 0x0 [IronBow] TwoActionAI
UNIT AeteriaStealable Mage 0x1 Level(6, Enemy, 1)     [15,4] 0b 0x0 0x0 0x0 [Thunder,PureWater] AttackInRangeAI
UNIT Aeteria Fighter 0x1 Level(6, Enemy, 1)     [10,5] 0b 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
UNIT Aeteria SCav 0x1 Level(6, Enemy, 1)     [9,3] 0b 0x0 0x0 0x0 [Armorslayer,Vulnerary] AttackInRangeAI
UNIT Aeteria PegRider 0x1 Level(6, Enemy, 1)     [8,13] 0b 0x0 0x0 0x0 [IronLance] StallOneTurnAI
UNIT Aeteria PegRider 0x1 Level(6, Enemy, 1)     [17,1] 0b 0x0 0x0 0x0 [SteelLance] ChargeAI
UNIT Aeteria Pirate 0x1 Level(6, Enemy, 1)     [15,9] 0b 0x0 0x0 0x0 [ShortAxe,] BossAI
UNIT Aeteria Archer 0x1 Level(6, Enemy, 1)     [4,3] 0b 0x0 0x0 0x0 [KillerBow] ChargeAI
	UNIT
	
RecruitsAgain:
UNIT Lennard Deserter 0x1 Level(6, Ally, 0)     [7,4] 0b 0x0 0x0 0x0 [SapphireLance,ShortSpear] [0x0,0x0,0x0,0x0]
UNIT Therese SArmor 0x1 Level(7, Ally, 0)     [5,4] 0b 0x0 0x0 0x0 [SteelBlade,Mine] [0x0,0x0,0x0,0x0]
	UNIT
	
WyvernAndPeg:
UNIT FlierSoul WyvRider 0x1 Level(6, Enemy, 1)     [8,18] 0b 0x0 0x0 0x0 [IronAxe] TwoActionAI
UNIT FlierSoul PegRider 0x1 Level(6, Enemy, 1)     [17,18] 0b 0x0 0x0 0x0 [Axereaver,IronLance] TwoActionAI
	UNIT	

LeftStairsRein1:
UNIT Aeteria Occultist 0x1 Level(6, Enemy, 1)     [0,3] 0b 0x0 0x0 0x0 [Flux] ChargeAI
UNIT Aeteria Occultist 0x1 Level(6, Enemy, 1)     [0,4] 0b 0x0 0x0 0x0 [DarkTome2] ChargeAI
	UNIT
	
MidReinWith1Unit:
UNIT Aeteria Myrmidon 0x1 Level(7, Enemy, 1)     [13,11] 0b 0x0 0x0 0x0 [IronSword,Lancereaver] [0,0xF,4,0]
	UNIT
	
LeftStairsRein3:
UNIT Aeteria Archer 0x1 Level(7, Enemy, 1)     [0,12] 0b 0x0 0x0 0x0 [SteelBow] ChargeAI
UNIT Aeteria Mercenary 0x1 Level(6, Enemy, 1)     [0,12] 0 0 1 REDA1R12 [IronSword,Zanbato] ChargeAI
	UNIT
	
LeftMoatRein1:
UNIT Aeteria Pirate 0x1 Level(6, Enemy, 1)     [18,0] 0b 0x0 0x0 0x0 [IronAxe] ChargeAI
	UNIT
	
LeftMoatRein2:
UNIT Aeteria Occultist 0x1 Level(6, Enemy, 1)     [0,9] 0b 0x0 0x0 0x0 [Flux] ChargeAI
UNIT Aeteria Occultist 0x1 Level(6, Enemy, 1)     [0,9] 0 0 1 REDA1R9 [DarkTome2] ChargeAI
	UNIT
	
LeftMoatRein3:
UNIT Aeteria Occultist 0x1 Level(7, Enemy, 1)     [0,9] 0b 0x0 0x0 0x0 [DarkTome2] ChargeAI
UNIT FlierSoul PegRider 0x1 Level(6, Enemy, 1)     [0,9] 0 0 1 REDA1R9 [IronLance] ChargeAI
	UNIT
	
ThiefRein1:
UNIT 0x64 Thief 0x1 Level(5, Enemy, 1)     [15,6] 0b 0x0 0x0 0x0 [Lancereaver,SteelDagger,ChestKey,ChestKey] ThiefAI
	UNIT
	
LeftStairsRein2:
UNIT InfantrySoul Fighter 0x1 Level(7, Enemy, 1)     [0,12] 0 0 1 REDA1R12 [Swordreaver] ChargeAI
UNIT InfantrySoul Soldier 0x1 Level(6, Enemy, 1)     [0,12] 0b 0x0 0x0 0x0 [Axereaver,SteelLance] ChargeAI
	UNIT
	
TopMoat1:
UNIT Aeteria Occultist 0x1 Level(6, Enemy, 1)     [17,0] 0b 0x0 0x0 0x0 [DarkTome2] StallOneTurnAI
UNIT FlierSoul WyvernRider 0x1 Level(7, Enemy, 1)     [18,0] 0b 0x0 0x0 0x0 [SteelAxe] ChargeAI
	UNIT
	
TopMoat2:
UNIT Aeteria Occultist 0x1 Level(7, Enemy, 1)     [18,0] 0b 0x0 0x0 0x0 [DarkTome1] ChargeAI
UNIT Aeteria Pirate 0x1 Level(7, Enemy, 1)     [17,0] 0b 0x0 0x0 0x0 [IronAxe] ChargeAI
	UNIT
	
TopMoat3:
UNIT FlierSoul PegRider 0x1 Level(7, Enemy, 1)     [18,0] 0b 0x0 0x0 0x0 [HeavySpear] ChargeAI
	UNIT
	
BottomLeftStairs:
UNIT FlierSoul WyvernRider 0x1 Level(7, Enemy, 1)     [17,0] 0b 0x0 0x0 0x0 [SteelAxe,Hammer] ChargeAI
	UNIT
	
ANOTHERWYVERN:
UNIT FlierSoul WyvernRider 0x1 Level(6, Enemy, 1)     [5,18] 0 0 1 REDA5R17 [SteelAxe] ChargeAI
	UNIT
	
InnerFortRein1:
UNIT Aeteria Shaman 0x1 Level(7, Enemy, 1)     [13,11] 0b 0x0 0x0 0x0 [DarkTome2] [0,0xF,4,0]
UNIT Aeteria Fighter 0x1 Level(6, Enemy, 1)     [13,11] 0 0 1 REDA14R11 [IronLabrys] [0,0xF,4,0]
	UNIT

InnerFortRein2:
UNIT InfantrySoul Archer 0x1 Level(6, Enemy, 1)     [13,11] 0b 0x0 0x0 0x0 [SteelBow] [0,0xF,4,0]
UNIT CavSoul LCav 0x1 Level(7, Enemy, 1)     [13,11] 0 0 1 REDA14R11 [Horseslayer] [0,0xF,4,0]
	UNIT
	
LeftFortRein1:
UNIT Aeteria Myrmidon 0x1 Level(6, Enemy, 1)     [5,18] 0b 0x0 0x0 0x0 [IronSword] [0,0xF,4,0]
	UNIT
	
LeftFortRein2:
UNIT Aeteria Fighter 0x1 Level(7, Enemy, 1)     [5,18] 0b 0x0 0x0 0x0 [SteelLabrys,Swordreaver] [0,0xF,4,0]
UNIT Aeteria Hunter 0x1 Level(7, Enemy, 1)     [5,18] 0 0 1 REDA3R18 [Longbow,SteelBow] [0,0xF,4,0]
	UNIT
	
RightFortRein1:
UNIT InfantrySoul Mercenary 0x1 Level(7, Enemy, 1)     [18,16] 0b 0x0 0x0 0x0 [IronSword] [0,0xF,4,0]
	UNIT
	
RightFortRein2:
UNIT Aeteria Thaumaturge 0x1 Level(6, Enemy, 1)     [18,16] 0 0 1 REDA19R17 [Iceberg] TwoActionAI
	UNIT
	
RightStairsRein1:
UNIT Aeteria Thaumaturge 0x1 Level(7, Enemy, 1)     [9,13] 0b 0x0 0x0 0x0 [Thunder] [0,0xF,4,0]
	UNIT
	
RightStairsRein2:
UNIT InfantrySoul Myrm 0x1 Level(7, Enemy, 1)     [9,13] 0 0 1 REDA9R12 [Flamberge] ChargeAI
UNIT CavSoul ACav 0x1 Level(7, Enemy, 1)     [9,13] 0b 0x0 0x0 0x0 [Halberd] ChargeAI
	UNIT
	
RightFortRein3:
UNIT AeteriaStealable Mage 0x1 Level(7, Enemy, 1)     [18,16] 0b 0x0 0x0 0x0 [Wind,Talisman] [0,0xF,4,0]
	UNIT
	
RightFortRein4:
UNIT InfantrySoul Soldier 0x1 Level(7, Enemy, 1)     [18,16] 0b 0x0 0x0 0x0 [0xDD,Javelin] TwoActionAI
	UNIT
	
HolyFuckWhy1:
UNIT InfantrySoul Soldier 0x1 Level(6, Enemy, 1)     [18,16] 0 0 1 REDA21R16 [IronLance] AttackInRangeAI
UNIT AeteriaStealable Thaumaturge 0x1 Level(6, Enemy, 1)     [18,16] 0 0 1 REDA21R18 [Fire,PureWater] StallOneTurnAI
UNIT MagSoul Druid 0x1 Level(1, Enemy, 1)     [18,16] DropItem 0 1 REDA19R17 [Flux, 0xCE] BossAI
UNIT InfantrySoul Mercenary 0x1 Level(7, Enemy, 1)     [18,16] 0 0 1 REDA16R17 [SteelSword,] AttackInRangeAI
	UNIT

HolyFuckWhy2:
UNIT MagSoul Sage 0x1 Level(1, Enemy, 1)     [5,18] DropItem 0 1 REDA4R17 [Fire,MasterSeal] BossAI
UNIT CavSoul SwordCav 0x1 Level(6, Enemy, 1)     [5,18] 0 0 1 REDA1R16 [SteelSword] TwoActionAI
UNIT CavSoul LanceCav 0x1 Level(6, Enemy, 1)     [5,18] 0 0 1 REDA0R18 [IronLance] ChargeAI
UNIT CavSoul PegRider 0x1 Level(6, Enemy, 1)     [5,18] 0 0 1 REDA7R18 [IronLance] TwoActionAI
UNIT InfantrySoul Archer 0x1 Level(7, Enemy, 1)     [5,18] 0 0 1 REDA2R18 [BodkinRifle,] AttackInRangeAI
	UNIT

HolyFuckWhy3:
UNIT MagSoul Bishop 0x1 Level(1, Enemy, 1)     [13,11] DropItem 0 1 REDA12R10 [Light1,EnergyRing] BossAI
UNIT InfantrySoul Archer 0x1 Level(6, Enemy, 1)     [13,11] 0 0 1 REDA16R12 [SteelBow] AttackInRangeAI
	UNIT

RedSussy:
UNIT King KingClass 0x1 Level(15, NPC, 0)     [6,1] 0b 0x0 0x0 0x0 [SteelBlade,SteelGlaive,SteelLabrys,0xD2] BossAI
UNIT Gorlois Parson 0x1 Level(7, NPC, 0)     [6,3] 0b 0x0 0x0 0x0 [0x40, Elixir] BossAI
UNIT Lili Monk 0x1 Level(5, Ally, 0)     [7,2] 0b 0x0 0x0 0x0 [Light1,Vulnerary] [0x0,0x0,0x0,0x0]
	UNIT
	
CutsceneUnits:
UNIT Affogato Hunter 0x1 Level(1, Ally, 1)     [1,18] 0 0 1 REDA1R17 [SteelBlade,SteelBlade,SteelBlade,SteelBlade] [0x0,0x0,0x0,0x0]
UNIT Ulrich LPaladin 0x1 Level(1, Ally, 1)     [2,18] 0 0 1 REDA2R17 [SteelBlade,SteelBlade,SteelBlade,SteelBlade] [0x0,0x0,0x0,0x0]
	UNIT
	
LeCutscene:
UNIT Aoife Sage_F 0x1 Level(12, Enemy, 0)     [12,18] 0b 0x0 0x0 0x0 [] BossAI
	UNIT

Traps1:
GreenSigil(17,2)
GreenSigil(18,2)
BlueSigil(13,11)
BlueSigil(5,18)
BlueSigil(18,16)
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4
	
FortniteLeft:
SVAL 0x2 0x0
CALL 0x9EE1B8
STAL 10
SOUN 0x60
RemoveTrapAtCoords(5,18)
CameraCursor(King)
STAL 60
CURE
TEXTSHOW KingLeftFort
TEXTEND
REMA
STAL 45
CHECK_EVENTID 0xB
BEQ 0x0 0xC 0x0
CHECK_EVENTID 0xC
BEQ 0x0 0xC 0x0
ENUT 0x3
CALL (EndingScene)
LABEL 0x0
EVBIT_T 0x7
NoFade
ENDA

FortniteMid:
SVAL 0x2 0x0
CALL 0x9EE1B8
STAL 10
SOUN 0x60
RemoveTrapAtCoords(13,11)
CameraCursor(King)
STAL 60
CURE
TEXTSHOW KingMidFort
TEXTEND
REMA
STAL 45
CHECK_EVENTID 0xA
BEQ 0x0 0xC 0x0
CHECK_EVENTID 0xC
BEQ 0x0 0xC 0x0
ENUT 0x3
CALL (EndingScene)
LABEL 0x0
EVBIT_T 0x7
NoFade
ENDA

FortniteRight:
SVAL 0x2 0x0
CALL 0x9EE1B8
STAL 10
SOUN 0x60
RemoveTrapAtCoords(18,16)
CameraCursor(King)
STAL 60
CURE
TEXTSHOW KingRightFort
TEXTEND
REMA
STAL 45
CHECK_EVENTID 0xA
BEQ 0x0 0xC 0x0
CHECK_EVENTID 0xB
BEQ 0x0 0xC 0x0
ENUT 0x3
CALL (EndingScene)
LABEL 0x0
EVBIT_T 0x7
NoFade
ENDA

KillTopMoat:
SVAL 0x2 0x0
CALL 0x9EE1B8
SOUN 0x2D2
TILECHANGE 0x1
RemoveTrapAtCoords(17,2)
RemoveTrapAtCoords(18,2)
CHECK_ALIVE Gorlois
BEQ 1 0x0 0xC
CUMO Affogato
STAL 60
CURE
TEXTSHOW GorloisWaterwaysDialogue
TEXTEND
REMA
GOTO 2
LABEL 1
STAL 60
CURE
TEXTSHOW AffoWaterwaysDialogue
TEXTEND
LABEL 2
NoFade
ENDA

Turn1WTF:
STAL 3
CAM1 [13,11]
FlashCursor(13,11,60)
STAL 5
CAM1 [5,18]
FlashCursor(5,18,60)
STAL 5
CAM1 [18,16]
FlashCursor(18,16,60)
STAL 3
CameraCursor(Affogato)
Text(Ch6Warning1)
NoFade
ENDA

LMoatEvent1:
MUSS 0x107
WORD 0x22820
CHECK_EVENTID 0xC
BNE 0x1 0xC 0x0
SpawnReinforcements(HolyFuckWhy1)
LABEL 0x1

STAL 10

CHECK_EVENTID 0xC
BNE 0x2 0xC 0x0
SpawnReinforcements(HolyFuckWhy2)
LABEL 0x2

STAL 10

CHECK_EVENTID 0xB
BNE 0x3 0xC 0x0
SpawnReinforcements(HolyFuckWhy3)
LABEL 0x3

STAL 15
CameraCursor(Affogato)

CHECK_EVENTID 0xA1
BEQ 0x776 0xC 0x0
Text(Ch6WTFMoment)
GOTO 0x420
LABEL 0x776
Text(Ch6WTFAltMoment)
LABEL 0x420
STAL 3
ENUT 0x4
NoFade
ENDA

LMoatEvent2:
CHECK_EVENTID 0xD
BNE 0x0 0xC 0x0
SpawnReinforcements(LeftMoatRein1)
LABEL 0x0
STAL 60
SpawnReinforcements(LeftMoatRein2)
NoFade
ENDA

LMoatEvent3:
SpawnReinforcements(LeftMoatRein3)
NoFade
ENDA

TMoatEvent1:
CHECK_EVENTID 0xD
BNE 0x0 0xC 0x0
SpawnReinforcements(TopMoat1)
STAL 60
SpawnReinforcements(LeftStairsRein1)
LABEL 0x0
CHECK_EVENTID 0xA
BNE 0x1 0xC 0x0
SpawnReinforcements(ANOTHERWYVERN)
LABEL 0x1
NoFade
ENDA

MidOneEvent1:
WORD 0x12820
CHECK_EVENTID 0xB
BNE 0x0 0xC 0x0
SpawnReinforcements(MidReinWith1Unit)
STAL 60
SpawnReinforcements(LeftStairsRein3)
LABEL 0x0
NoFade
ENDA

TMoatEvent2:
CHECK_EVENTID 0xD
BNE 0x0 0xC 0x0
SpawnReinforcements(TopMoat2)
STAL 60
LABEL 0x0
CHECK_EVENTID 0xD
BNE 0x1 0xC 0x0
SpawnReinforcements(BottomLeftStairs)
LABEL 0x1
SpawnReinforcements(ThiefRein1)
NoFade
ENDA

TMoatEvent3:
CHECK_EVENTID 0xD
BNE 0x0 0xC 0x0
SpawnReinforcements(TopMoat3)
STAL 60
LABEL 0x0
SpawnReinforcements(LeftStairsRein2)
NoFade
ENDA

LRFortEvent1:
CHECK_EVENTID 0xA
BNE 0x0 0xC 0x0
SpawnReinforcements(LeftFortRein1)
LABEL 0x0
CHECK_EVENTID 0xC
BNE 0x1 0xC 0x0
SpawnReinforcements(RightFortRein1)
LABEL 0x1
NoFade
ENDA

LFortEvent1:
CHECK_EVENTID 0xA
BNE 0x0 0xC 0x0
SpawnReinforcements(LeftFortRein2)
LABEL 0x0
NoFade
ENDA

RFortEvent2:
CHECK_EVENTID 0xB
BNE 0x0 0xC 0x0
SpawnReinforcements(RightFortRein2)
STAL 60
LABEL 0x0
SpawnReinforcements(WyvernAndPeg)
STAL 60
SpawnReinforcements(RightFortRein1)
NoFade
ENDA

RFortEvent3:
CHECK_EVENTID 0xC
BNE 0x0 0xC 0x0
SpawnReinforcements(RightFortRein3)
LABEL 0x0
NoFade
ENDA

MFortEvent1:
WORD 0x02820
CHECK_EVENTID 0xB
BNE 0x0 0xC 0x0
SpawnReinforcements(InnerFortRein1)
STAL 60
LABEL 0x0
SpawnReinforcements(RightStairsRein2)
CHECK_EVENTID 0xC
BNE 0x1 0xC 0x0
SpawnReinforcements(RightFortRein4)
LABEL 0x1
NoFade
ENDA

MFortEvent2:
CHECK_EVENTID 0xB
BNE 0x0 0xC 0x0
SpawnReinforcements(InnerFortRein2)
LABEL 0x0
NoFade
ENDA

TriggerHappyHavoc:
ENUT 0x3
CALL (EndingScene)
ENDA

BeginningScene:
	LOAD1 0x1 RedSussy
	ENUN
SetBackground(0x35) //black
Text(Ch6Opening0)
STAL 20
Music(0x48)
SetBackground(0x2B)
STAL 7
BROWNBOXTEXT PalaceOutskirtsText [8,8]
Text(Ch6Opening1)
FADI 60
CLEAN
FADU 60
STAL 5
BROWNBOXTEXT PalaceText [8,8]
STAL 15
Music(0x92)
STAL 15
LOAD2 0x1 CutsceneUnits
CameraCursor(Affogato)
STAL 15
SetBackground(0x40)
DoubleConditionalDialog(Breve,Hildr,Ch6Opening2,Ch6OpeningAlt2,3,4)
STAL 30
Music(0x24)
Text(Introducethedarnedunitspls)
STAL 5
FADI 60
CLEAN
FADU 60
STAL 5
Music(0xED)
SetBackground(0x29)
STAL 5
BROWNBOXTEXT ThroneRoomText [8,8]
STAL 15
DoubleConditionalDialog(Brudet,Stroganoff,Ch6Opening3,Ch6OpeningAlt3,5,6)
STAL 5
FADI 60
CLEAN
	LOAD1 0x1 EnemyUnits
	LOAD1 0x1 RecruitsAgain
FADU 30
STAL 10
Music(0x93)
SetBackground(0x42)
TripleIfAliveDialog(Ansgar,Hildr,Breve,Ch6Opening4,7)
STAL 15
FADI 15
Music(0x28)
STAL 5
FADU 40
Text(Ch6Opening5)
STAL 5
FADI 60
CLEAN
FADU 40
STAL 20

CameraCursor(Gorlois)
Text(Ch6Opening6)
STAL 15
MUSI
	CenterTextbox(Ch6ObjectiveWarning)
	MUNO
	GotoPrepScreen
	ENDA
	
AffoGorloisConvo:
	MUSI
	Text(AffoGorloisTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
YoryLiliConvo:
	MUSI
	Text(YoryIrisTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
StroganSeqConvo:
	MUSI
	Text(SeqStroganTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
BrudetMochaConvo:
	MUSI
	Text(BrudetMochaTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
StroganBrudetConvo:
	MUSI
	Text(StroganBrudetTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
RinaHildrConvo:
	MUSI
	Text(RinaHildrTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
BreveFlorisConvo:
	MUSI
	Text(BreveFlorisTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
EisNoelConvo:
	MUSI
	Text(EisNoelTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
LennardPandoroConvo:
	MUSI
	Text(LennardPandoroTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
DoppioBrudetConvo:
	MUSI
	Text(DoppioBrudetTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
IronRuneConvo:
	MUSI
	Text(StroganGorloisTalk)
	MUNO
	MURE 2
GiveItem(Lightrune, Stroganoff)
	NoFade
	ENDA
	
AffoKingConvo:
	MUSI
	CHECK_EVENTID 0x78
	BEQ 1 0x0 0xC
	Text(KingAffoTalk)
	GiveItem(GoddessIcon, Affo)
	GOTO 2
	LABEL 1
	Text(KingAffoTalkAlt)
	LABEL 2
	MUNO
	MURE 2
	UnsetFlag(0x78)
	NoFade
	ENDA
	
NoraKingConvo:
	MUSI
	CHECK_EVENTID 0x79
	BEQ 1 0x0 0xC
	Text(NoraKingTalk)
	GiveItem(Rescue, Nora)
	GOTO 2
	LABEL 1
	Text(NoraKingTalkAlt)
	LABEL 2
	MUNO
	MURE 2
	UnsetFlag(0x79)
	NoFade
	ENDA
	
AnsgarThereseConvo:
	MUSI
CharacterConditionalDialog(Stroganoff,AnsgarThereseTalk,AnsgarThereseTalkAlt)
	MUNO
	MURE 2
	NoFade
	ENDA

EndingScene:
SetFlag(0x84) //hides boss icons
CAM1 [12,16]
WORD 0x12820 //snow weather
STAL 45
LOAD1 0x1 LeCutscene
WORD 0x2820 //sunny weather
STAL 45
CUMO 0x34
STAL 60
CURE
SOUN 0xF2
WORD 0x52820 //fire weather
MUSI
STAL 30
Text(Ch6Ending0)
MUNO
Music(0x26)
MOVE 0xA Aoife [14,18]
ENUN
DISA_IF 0x34
STAL 10
CAM1 [6,3]
LOAD2 0x1 LeCutscene
ENUN
SVAL 0x2 Aoife
_WARP 0xFFFF 0xFFFD [5,2]
CALL 0x9EE4F8
_WARP 0x0 Aoife [5,2]
STAL 15
Text(Ch6Ending0Again)
STAL 5
SVAL 0x2 Aoife
CALL 0x9EE51C
DISA Aoife
EVBIT_T 0x7

FADI 60
STAL 50
DoAllThree
Music(0x3E4)
SetBackground(0x29)
STAL 5
BROWNBOXTEXT ThroneRoomText [8,8]
STAL 15
TEXTSTART
TEXTSHOW Ch6Ending1
TEXTEND
TEXTSHOW AffoInterrupt
TEXTEND

CHECK_ALIVE Breve
BEQ 0x9B 0xC 0x0
	TEXTSHOW2 BreveInterrupt
	TEXTEND
LABEL 0x9B
CHECK_ALIVE Hildr
BEQ 0x9C 0xC 0x0
	TEXTSHOW2 HildrInterrupt
	TEXTEND
LABEL 0x9C
CHECK_ALIVE Ansgar
BEQ 0x9D 0xC 0x0
	TEXTSHOW2 AnsgarInterrupt
	TEXTEND
LABEL 0x9D
CHECK_ALIVE Nora
BEQ 0x9E 0xC 0x0
	TEXTSHOW2 NoraInterrupt
	TEXTEND
LABEL 0x9E
CHECK_ALIVE Sequin
BEQ 0x9F 0xC 0x0
	TEXTSHOW2 SequinInterrupt
	TEXTEND
LABEL 0x9F
CHECK_ALIVE Brudet
BEQ 0x17 0xC 0x0
	TEXTSHOW2 BrudetInterrupt
	TEXTEND
LABEL 0x17
CHECK_ALIVE Stroganoff
BEQ 0x18 0xC 0x0
	TEXTSHOW2 StrogyInterrupt
	TEXTEND
LABEL 0x18
CHECK_ALIVE Doppio
BEQ 0x19 0xC 0x0
	TEXTSHOW2 DoppioInterrupt
	TEXTEND
LABEL 0x19
CHECK_ALIVE Eiskaffee
BEQ 0x1A 0xC 0x0
	TEXTSHOW2 EisInterrupt
	TEXTEND
LABEL 0x1A
CHECK_ALIVE Mocha
BEQ 0x1B 0xC 0x0
	TEXTSHOW2 MochaInterrupt
	TEXTEND
LABEL 0x1B
CHECK_ALIVE Pandoro
BEQ 0x1C 0xC 0x0
	TEXTSHOW2 PandoroInterrupt
	TEXTEND
LABEL 0x1C
CHECK_ALIVE Rina
BEQ 0x1D 0xC 0x0
	TEXTSHOW2 RinaInterrupt
	TEXTEND
LABEL 0x1D
CHECK_ALIVE Noel
BEQ 0x1E 0xC 0x0
	TEXTSHOW2 NoelInterrupt
	TEXTEND
LABEL 0x1E
CHECK_ALIVE Floris
BEQ 0x1F 0xC 0x0
	TEXTSHOW2 FlorisInterrupt
	TEXTEND
LABEL 0x1F
CHECK_ALIVE Yory
BEQ 0x20 0xC 0x0
	TEXTSHOW2 YoryInterrupt
	TEXTEND
LABEL 0x20
CHECK_ALIVE Therese
BEQ 0x21 0xC 0x0
	TEXTSHOW2 ThereseInterrupt
	TEXTEND
LABEL 0x21
CHECK_ALIVE Lennard
BEQ 0x22 0xC 0x0
	TEXTSHOW2 LennardInterrupt
	TEXTEND
LABEL 0x22
CHECK_ALIVE Iris
BEQ 0x23 0xC 0x0
	TEXTSHOW2 IrisInterrupt
	TEXTEND
LABEL 0x23
CHECK_ALIVE Kane
BEQ 0x24 0xC 0x0
	TEXTSHOW2 KaneInterrupt
	TEXTEND
LABEL 0x24

TEXTSHOW GorloisContinue
TEXTEND

STAL 20
Music(0x115)
FADI 45
CLEAN
SetBackground(0x35)
SetBackground(0x30)
STAL 5
BROWNBOXTEXT FewHoursLaterText [8,8]
STAL 15
Text(Ch6Ending2)
STAL 10
Music(0xa9)
FADI 45
CLEAN
SetBackground(0x35)
SetBackground(0x2d)
STAL 5
BROWNBOXTEXT AeteriaCapitalText [8,8]
STAL 15
Text(Ch6Ending0AgainAgain)
STAL 35
FADI 45
CLEAN
SetBackground(0x35)
SetBackground(0x45)
Music(0x4A)
STAL 5
BROWNBOXTEXT HallText [8,8]
STAL 15
TEXTSTART
TEXTSHOW Ch6Ending4
TEXTEND
MUSC 0x107
TEXTCONT
TEXTEND
STAL 15
FADI 60

UnsetFlag(0x84)
// reset the party for part 2
QuickRemu(Affogato,1)
QuickRemu(Ulrich,2)
QuickRemu(Evelyn,3)
QuickRemu(Sequin,4)
QuickRemu(Breve,5)
QuickRemu(Hildr,6)
QuickRemu(Ansgar,7)
QuickRemu(Latte,8)
QuickRemu(Irene,9)
QuickRemu(Pandoro,10)
QuickRemu(Mocha,11)
QuickRemu(Tammy,12)
QuickRemu(Stroganoff,13)
QuickRemu(Eiskaffee,14)
QuickRemu(Doppio,15)
QuickRemu(Floris,16)
QuickRemu(Atheya,17)
QuickRemu(Noel,18)
QuickRemu(Therese,19)
QuickRemu(Lennard,20)
QuickRemu(Yory,21)
QuickRemu(Lili,22)
MUSCMID 0x7FFF
STAL 120
EVBIT_F 0x2
CLEAN
SetBackground(0x35)
//MNC2 0x7 //next chapter
MNTS 0x1 //return to title screen
	NoFade
	ENDA


