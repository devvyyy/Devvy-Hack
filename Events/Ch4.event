EventPointerTable(Ch4Event, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
TURN 0x0 GamerTime [1,0] 0x0
TurnEventEnemy(0,HartWarns,1)
TURN 0x0 Turn2Rein [2,0] 0x0
TURN 0x0 Turn3Rein [3,0] 0x0
TURN 0x0 Turn4Rein [4,0] 0x0
TURN 0x0 Turn5Rein [5,0] 0x0
TURN 0x0 Turn6Rein [6,0] 0x0
TURN 0x0 Turn7Rein [7,0] 0x0
TURN 0x0 Turn8Rein [8,0] 0x0
TURN 0x0 Turn9Rein [9,0] 0x0
TURN 0x3 EndingScene [11,0] 0x0
	END_MAIN

ALIGN 32
CharacterBasedEvents:
CharacterEvent(0x0,YoryTurretCheck,Yory,0x3F)
CharacterEventBothWays(0x9, NoraFlorisConvo, Nora, Floris)
CharacterEventBothWays(0x10, YoryKaneConvo, Yory, Kane)
CharacterEventBothWays(0x11, FlorisUlrichConvo, Floris, Ulrich)
	END_MAIN

MiscBasedEvents:
	CauseGameOverIfLordDies
DefeatBoss(EndingScene)
AREA 0x65 WeAreDead [0,11] [0,13]
AREA 0x65 WeAreDead [9,15] [10,15]
AREA 0x65 WeAreDead [16,11] [16,12]
	END_MAIN

LocationBasedEvents:
Door(8,1)
Door(4,2)
Chest(Illuminate,16,4)
Chest(MageLance,2,3)
DurabilityChest(0xBC,0x0F,1,4)
	END_MAIN

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
LoadUnit(1,12)
LoadUnit(2,12)
LoadUnit(2,11)
LoadUnit(1,11)
LoadUnit(9,13)
LoadUnit(14,11)
LoadUnit(15,11)
LoadUnit(10,13)
LoadUnit(14,12)
LoadUnit(15,12)
LoadUnit(9,12)
LoadUnit(10,12)
	UNIT

EnemyUnits:
UNIT Hartmann Druid 0x1 Level(6, Enemy, 0)     [1,1] 0b 0x0 0x0 0x0 [DarkTome5,AngelicRobe] BossAI
UNIT Aeteria Thaumaturge 0x1 Level(4, Enemy, 1)     [0,3] 0b 0x0 0x0 0x0 [Fire,] DefendAI
UNIT Aeteria Shaman 0x1 Level(5, Enemy, 1)     [5,2] DropItem 0x0 0x0 0x0 [DarkTome2,DoorKey] BossAI
UNIT Aeteria LArmor 0x1 Level(4, Enemy, 1)     [9,1] DropItem 0x0 0x0 0x0 [IronGlaive,ShortSpear,DoorKey] BossAI
UNIT Aeteria Shaman 0x1 Level(4, Enemy, 1)     [11,2] 0b 0x0 0x0 0x0 [Flux,] DefendAI	
UNIT FlyingMag Occultist 0x1 Level(4, Enemy, 1)     [16,0] 0b 0x0 0x0 0x0 [Flux] TwoActionAI
UNIT FlyingMagStealable Occultist 0x1 Level(5, Enemy, 1)     [9,5] 0b 0x0 0x0 0x0 [Flux,Talisman] StallOneTurnAI
UNIT UPirate Pirate 0x1 Level(4, Enemy, 1)     [15,0] 0b 0x0 0x0 0x0 [SteelAxe] StallOneTurnAI
UNIT Aeteria Soldier 0x1 Level(4, Enemy, 1)     [14,8] 0b 0x0 0x0 0x0 [IronLance] DefendAI
UNIT Aeteria Mercenary 0x1 Level(4, Enemy, 1)     [15,4] 0b 0x0 0x0 0x0 [IronSword,] StallOneTurnAI
UNIT Aeteria Mercenary 0x1 Level(4, Enemy, 1)     [6,9] DropItem 0x0 0x0 0x0 [Zanbato,Mine] DefendAI
UNIT Aeteria Fighter 0x1 Level(4, Enemy, 1)     [3,10] 0b 0x0 0x0 0x0 [IronAxe,] DefendAI
UNIT Aeteria Shaman 0x1 Level(4, Enemy, 1)     [5,12] 0b 0x0 0x0 0x0 [DarkTome2,] DefendAI
UNIT Aeteria Mage 0x1 Level(5, Enemy, 0)     [6,5] 0b 0x0 0x0 0x0 [AnimaTome1,] WallAI
UNIT Aeteria Mage 0x1 Level(4, Enemy, 0)     [12,0] 0b 0x0 0x0 0x0 [AnimaTome1,] TwoActionAI
UNIT Aeteria Shaman 0x1 Level(4, Enemy, 1)     [3,4] 0b 0x0 0x0 0x0 [Flux,] DefendAI
UNIT Aeteria Archer 0x1 Level(4, Enemy, 1)     [16,6] 0x0 0x0 0x0 0x0 [IronBow,] DefendAI
UNIT Aeteria Fighter 0x1 Level(4, Enemy, 1)     [12,5] 0x0 0x0 0x0 0x0 [IronAxe,] WallAI
UNIT Aeteria Mage 0x1 Level(5, Enemy, 1)     [11,10] DropItem 0x0 0x0 0x0 [Fire, 0x70] DefendAI
UNIT Aeteria Mercenary 0x1 Level(5, Enemy, 1)     [6,0] 0x0 0x0 0x0 0x0 [IronSword,] WallAI
	UNIT

NPCEPIC:
UNIT HolyGuard Monk 0x1 Level(3, NPC, 1)     [3,14] 0b 0x0 0x0 0x0 [Light1, Vulnerary] [0,0,2,0]
UNIT HolyGuard Archer 0x1 Level(3, NPC, 1)     [12,12] 0b 0x0 0x0 0x0 [Elixir] [0,3,0,32]
UNIT

NPCREIN:
UNIT HolyGuard Priest 0x1 Level(5, NPC, 1)     [9,15] 0 0 1 REDA9R13 [Fear, Vulnerary] [0,0,2,0]
UNIT HolyGuard Cleric 0x1 Level(5, NPC, 1)     [10,15] 0 0 1 REDA10R13 [Slow, Vulnerary] [0,0,2,0]
UNIT

TankyHealerWTF:
UNIT Brulee Priest 0x1 Level(4, Ally, 0)     [6,12] 0x0 0x0 0x0 0x0 [Mend,Haze] AttackInRangeAI
UNIT Yory 0x61 0x1 Level(3, Ally, 0)     [5,13] 0x0 0x0 0x0 0x0 [Dragonstone,0xF2,0xF3,Mine,] AttackInRangeAI
UNIT

ArenaRein:
UNIT Aeteria Shaman 0x1 Level(4, Enemy, 1)     [10,0] 0b 0x0 0x0 0x0 [DarkTome1,] DefendAI
UNIT FlyingMagStealable Thaumaturge 0x1 Level(4, Enemy, 1)     [11,0] 0b 0x0 0x0 0x0 [Fire,RedGem] StallOneTurnAI
UNIT

ArenaRein2:
UNIT Aeteria Soldier 0x1 Level(4, Enemy, 1)     [9,0] 0b 0x0 0x0 0x0 [IronLance,] DefendAI
UNIT Aeteria Fighter 0x1 Level(5, Enemy, 1)     [11,0] 0b 0x0 0x0 0x0 [SteelAxe,] DefendAI
UNIT

WaterRein:
UNIT UPirate Pirate 0x1 Level(4, Enemy, 1)     [15,0] 0b 0x0 0x0 0x0 [IronAxe] DefendAI
UNIT UPirate Pirate 0x1 Level(5, Enemy, 1)     [16,0] 0b 0x0 0x0 0x0 [SteelAxe] DefendAI
UNIT

WaterRein2:
UNIT FlyingMag Occultist 0x1 Level(4, Enemy, 1)     [15,0] 0b 0x0 0x0 0x0[Flux] ChargeAI
UNIT UPirate Pirate 0x1 Level(5, Enemy, 1)     [16,0] 0b 0x0 0x0 0x0 [SteelAxe] DefendAI
UNIT

MountRein:
UNIT FlyingMag Occultist 0x1 Level(4, Enemy, 1)     [0,7] 0 0 1 REDA2R7 [Flux] TwoActionAI
UNIT FlyingPhys PegRider 0x1 Level(5, Enemy, 1)     [0,7] 0b 0x0 0x0 0x0 [IronLance] ChargeAI
UNIT

MountRein2:
UNIT FlyingPhys PegRider 0x1 Level(6, Enemy, 1)     [16,8] DropItem 0x0 0x0 0x0 [SteelLance, Torch] ChargeAI
UNIT FlyingPhys PegRider 0x1 Level(5, Enemy, 1)     [16,7] 0b 0x0 0x0 0x0 [IronLance,Javelin] ChargeAI
UNIT

ThiefRein:
UNIT 0x64 Thief 0x1 Level(4, Enemy, 1)     [16,3] 0b 0x0 0x0 0x0 [IronDagger,ChestKey,] ThiefAI
UNIT

ThiefRein2:
UNIT 0x64 Thief 0x1 Level(5, Enemy, 1)     [0,4] 0b 0x0 0x0 0x0 [SteelSword,ChestKey,ChestKey] ThiefAI
UNIT

StairRein:
UNIT Aeteria Shaman 0x1 Level(6, Enemy, 1)     [9,0] DropItem 0x0 0x0 0x0 [Flux, PureWater] DefendAI
UNIT Aeteria Soldier 0x1 Level(5, Enemy, 1)     [12,0] 0b 0x0 0x0 0x0 [Javelin] DefendAI
UNIT

StairRein2:
UNIT Aeteria Archer 0x1 Level(5, Enemy, 1)     [5,0] 0b 0x0 0x0 0x0 [IronBow] WallAI
UNIT Aeteria Mage 0x1 Level(5, Enemy, 1)     [6,0] 0b 0x0 0x0 0x0 [AnimaTome1] DefendAI
UNIT

ArenaRein3:
UNIT Aeteria Mage 0x1 Level(5, Enemy, 1)     [16,3] 0b 0x0 0x0 0x0 [AnimaTome2] DefendAI
UNIT FlyingPhys PegRider 0x1 Level(6, Enemy, 1)     [0,7] 0 0 1 REDA1R8 [IronLance] StallOneTurnAI
UNIT

ArenaRein4:
UNIT Aeteria Myrmidon 0x1 Level(4, Enemy, 1)     [16,6] 0x0 0x0 0x0 0x0 [IronSword] DefendAI
UNIT

InsideRein:
UNIT Aeteria ACav 0x1 Level(5, Enemy, 1)     [0,3] 0b 0x0 0x0 0x0 [IronAxe,HandAxe] DefendAI
UNIT FlyingMag Occultist 0x1 Level(5, Enemy, 1)     [0,4] 0b 0x0 0x0 0x0 [DarkTome1,] WallAI
UNIT

InsideRein2:
UNIT FlyingMagStealable Occultist 0x1 Level(6, Enemy, 1)     [14,0] 0b 0x0 0x0 0x0 [DarkTome1,Vulnerary] WallAI
UNIT

FlierRein:
UNIT FlyingPhys PegRider 0x1 Level(5, Enemy, 1)     [13,0] 0b 0x0 0x0 0x0 [IronLance] ChargeAI
UNIT FlyingPhys PegRider 0x1 Level(6, Enemy, 1)     [14,0] 0 0 1 REDA14R1 [SteelLance] ChargeAI
UNIT

EndCutsceneUnits:
UNIT Affogato Hunter 0x1 Level(1, Ally, 1)     [9,13] 0b 0x0 0x0 0x0 [SteelBlade,SteelBlade,SteelBlade,SteelBlade] [0x0,0x0,0x0,0x0]
UNIT

EndCutsceneUnits2:
UNIT Ulrich LPaladin 0x1 Level(1, Ally, 1)     [10,14] 0b 0x0 0x0 0x0 [SteelBlade,SteelBlade,SteelBlade,SteelBlade] [0x0,0x0,0x0,0x0]
UNIT

Traps1:
BLST [12,12] Ballista
TelliusTorchOn(2,10,2)
TelliusTorchOn(15,10,2)
TelliusTorchOn(10,8,2)
TelliusTorchOn(2,1,2)
RedSigil(9,15)
RedSigil(10,15)
RedSigil(0,11)
RedSigil(0,12)
RedSigil(0,13)
RedSigil(16,11)
RedSigil(16,12)
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
SetBackground(0x3F)
Music(0x4E)
STAL 5
BROWNBOXTEXT NorthelmBorderText [8,8] // box in the top right that's pretty cool for small text
STAL 15

CHECK_ALIVE Stroganoff
BEQ 1 0x0 0xC

TEXTSTART
TEXTSHOW Ch4Opening1
TEXTEND
SOUN 0xA6
STAL 5
SOUN 0xA7
STAL 10
SOUN 0x268
STAL 15
MUSC 0x25
TEXTCONT
TEXTEND

GOTO 2
LABEL 1

TEXTSTART
TEXTSHOW Ch4OpeningAlt1
TEXTEND
SOUN 0xA6
STAL 5
SOUN 0xA7
STAL 10
SOUN 0x268
STAL 15
MUSC 0x25
TEXTCONT
TEXTEND

LABEL 2
FADI 60
CLEAN
	LOAD1 0x1 EnemyUnits
	LOAD1 0x1 TankyHealerWTF
SilentlyGiveItem(0xD6, Yory)
	LOAD1 0x1 NPCEPIC
	ENUN
FADU 45
STAL 15
CameraCursor(Floris)
MUSC 0x26
SetBackground(0x3F)
Text(Ch4Opening2)
STAL 10
FADI 60
CLEAN
FADU 60
STAL 5
MUSC 0x7FFF
CameraCursor(Hartmann)
FADI 60
MUSC 0x2E
SetBackground(0x27)
Text(Ch4Opening3)
FADI 60
CLEAN
FADU 45
MUSCMID 0x7FFF
STAL 20
	CenterTextbox(Ch4Warning)
	GotoPrepScreen
	ENDA

EndingScene:
ENUT 0x3
FADI 20
DoAllThree
SOUN 0x50
STAL 40
Music(VictoryBGM)
LOAD2 0x1 EndCutsceneUnits
FADU 12
CameraCursor(Affogato)
STAL 5
SetBackground(0x2B)
Text(Ch4Ending1)
FADI 60
CLEAN
Music(0x26)
FADU 16
STAL 5
BROWNBOXTEXT LaterThatNight [8,8] // box in the top right that's pretty cool for small text
STAL 15
MOVE 0xD Affogato [8,14]
ENUN
STAL 10
Text(Ch4Ending2)
SOUN 0xAF
STAL 10
MOVE 0xD Affogato [11,13]
ENUN
STAL 10
SOUN 0xAF
MOVE 0xD Affogato [9,12]
ENUN
SOUN 0xCC
STAL 15
LOAD2 0x1 EndCutsceneUnits2
STAL 20
Music(0x48)
SetBackground(0x0C)
CharacterConditionalDialog(Doppio,Ch4Ending3,Ch4EndingAlt3)
FADI 60
STAL 7
CLEAN
Music(0x63)
STAL 3
SetBackground(0x43)
STAL 5
BROWNBOXTEXT PalaceText [8,8] // box in the top right that's pretty cool for small text
STAL 15
Text(Ch4Ending4)
STAL 5
	MNC2 0x5
	NoFade
	ENDA
	
HartWarns:
CameraCursor(Hartmann)
Text(HartDarker)
NoFade
ENDA

Turn2Rein:
CamOn
LoadReinforcements(ArenaRein)
STAL 20
LoadReinforcements(ArenaRein4)
VCWF 6
STAL 30
NoFade
ENDA

Turn3Rein:
CamOn
LoadReinforcements(WaterRein)
STAL 20
LoadReinforcements(InsideRein)
STAL 20
CAM1 [9,13]
LOAD1 0x1 NPCREIN
Text(AffoReinText2)
VCWF 5
STAL 30
NoFade
ENDA

Turn4Rein:
CamOn
LoadReinforcements(FlierRein)
STAL 20
LoadReinforcements(ThiefRein)
VCWF 4
STAL 30
NoFade
ENDA

Turn5Rein:
CamOn
LoadReinforcements(MountRein)
STAL 20
LoadReinforcements(MountRein2)
VCWF 3
Text(HartBackup)
STAL 30
NoFade
ENDA

Turn6Rein:
CamOn
LoadReinforcements(ArenaRein3)
STAL 30
NoFade
ENDA

Turn7Rein:
CamOn
LoadReinforcements(StairRein)
STAL 20
LoadReinforcements(WaterRein2)
STAL 20
LoadReinforcements(ThiefRein2)
VCWF 2
CharacterConditionalDialog(Pandoro,AffoReinText,AffoReinTextAlt)
STAL 30
NoFade
ENDA

Turn8Rein:
CamOn
LoadReinforcements(StairRein2)
STAL 20
LoadReinforcements(InsideRein2)
STAL 30
NoFade
ENDA

Turn9Rein:
CamOn
LoadReinforcements(ArenaRein2)
VCWF 1
STAL 30
NoFade
ENDA

NoraFlorisConvo:
	MUSI
	Text(NoraFlorisTalk)
	MUNO
	MURE 2
	GiveItem(Barrier,Floris)
	ENUT 0x79
	NoFade
	ENDA

YoryKaneConvo:
	MUSI
	Text(YoryKaneTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
FlorisUlrichConvo:
	MUSI
	Text(UlrichFlorisTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
GamerTime:
CAM1 [0,12]
STAL 20
CAM1 [16,12]
STAL 3
	Text(OhNoBadGuysss)
	STAL 5
	CenterTextbox(RedTileDisclaimer)
NoFade
VCWF 7
ENDA

WeAreDead:
CHECK_ACTIVE
SVAL 1 Aeteria
BEQ 0x1 0xC 0x1
SVAL 1 UPirate
BEQ 0x1 0xC 0x1
ENUF 0x65
GOTO 0x2
LABEL 0x1
CAM1 Affogato
CUMO Affogato
STAL 60
CURE
Text(WTFWeAreDead)
GameOver
LABEL 0x2
NoFade
ENDA

