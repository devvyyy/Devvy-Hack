EventPointerTable(Ch5xEvent, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
TURN 0x0 KillVillageBrigand [2,0] 0x0
TURN 0x0 RecruitThief [2,0] 0x80
TURN 0x0 ReinforcementsWarning [3,0] 0x0
TURN 0x0 KillVillageBrigand2 [3,0] 0x0
TURN 0x0 LeftRocksRein [4,0] 0x0
TURN 0x0 BottomRightRein [5,0] 0x0
TURN 0x0 TopRightRein [6,0] 0x0
TURN 0x0 DoubleDeath [7,0] 0x0
TURN 0x0 TopLeftPegasi [9,0] 0x0
	END_MAIN

ALIGN 32
CharacterBasedEvents:
CharacterEventBothWays(0xC, MochaRinaConvo, Mocha, Rina)
CharacterEventBothWays(0xD, AffoUlrichCh5Convo, Affogato, Ulrich)
CharacterEventBothWays(0xE, EisDoppioConvo, Doppio, Eiskaffee)
CharacterEventBothWays(0x20, RecruitRecruitThief, 0x0, Noel)
	END_MAIN

MiscBasedEvents:
	DefeatBoss(EndingScene)
	CauseGameOverIfLordDies
	END_MAIN

LocationBasedEvents:
Village(0x8,JavelinVillage,13,6)
Village(0x9,SBookVillage,1,14)
House(0x0,House1,6,17)
House(0x0,House2,13,16)
Vendor(ShopAwesome,6,12)
	END_MAIN

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
LoadUnit(0,1)
LoadUnit(1,1)
LoadUnit(2,1)
LoadUnit(2,2)
LoadUnit(0,2)
LoadUnit(1,2)
LoadUnit(13,1)
LoadUnit(14,1)
LoadUnit(13,2)
LoadUnit(14,2)
LoadUnit(12,1)
LoadUnit(12,2)
	UNIT

EnemyUnits:
UNIT Hiems Shaman 0x1 Level(5, Enemy, 1)     [6,2] 0b 0x0 0x0 0x0 [Flux,] AttackInRangeAI
UNIT BadGuy LArmor 0x1 Level(6, Enemy, 1)     [11,5] 0b 0x0 0x0 0x0 [IronGlaive,ShortSpear] BossAI
UNIT Hiems Shaman 0x1 Level(5, Enemy, 1)     [7,7] 0b 0x0 0x0 0x0 [Flux,] ChargeAI
UNIT BadGuy Mercenary 0x1 Level(5, Enemy, 1)     [5,4] 0b 0x0 0x0 0x0 [IronBlade,] AttackInRangeAI
UNIT BadGuy Freelancer 0x1 Level(6, Enemy, 1)     [6,10] 0b 0x0 0x0 0x0 [IronLance] TwoActionAI
UNIT BadGuy AxeCav 0x1 Level(5, Enemy, 1)     [0,11] 0b 0x0 0x0 0x0 [IronAxe] TwoActionAI
UNIT BadGuy LanceCav 0x1 Level(5, Enemy, 1)     [3,15] 0b 0x0 0x0 0x0 [HeavySpear] StallOneTurnAI
UNIT BadGuyStealable SArmor 0x1 Level(5, Enemy, 1)     [3,18] 0b 0x0 0x0 0x0 [SteelBlade,Elixir] TwoActionAI
UNIT BadGuyStealable Hunter 0x1 Level(5, Enemy, 1)     [4,12] 0b 0x0 0x0 0x0 [Longbow,RedGem] BossAI
UNIT BadGuy AxeArmor 0x1 Level(5, Enemy, 1)     [9,12] 0b 0x0 0x0 0x0 [Swordreaver,HandAxe] BossAI
UNIT BadGuy Myrm 0x1 Level(5, Enemy, 1)     [8,15] 0b 0x0 0x0 0x0 [Zanbato] AttackInRangeAI
UNIT Hiems Shaman 0x1 Level(6, Enemy, 1)     [6,18] 0b 0x0 0x0 0x0 [DarkTome2] AttackInRangeAI
UNIT BadGuy SwordCav 0x1 Level(6, Enemy, 1)     [14,19] 0b 0x0 0x0 0x0 [IronSword,Vulnerary] ChargeAI
UNIT Hiems Mage 0x1 Level(5, Enemy, 1)     [9,4] 0b 0x0 0x0 0x0 [Fire] TwoActionAI
UNIT BadGuy Mercenary 0x1 Level(5, Enemy, 1)     [10,9] 0b 0x0 0x0 0x0 [IronSword,] AttackInRangeAI
UNIT BadGuy Hunter 0x1 Level(5, Enemy, 1)     [0,19] 0b 0x0 0x0 0x0 [IronBow] AttackInRangeAI
UNIT BadGuy Hunter 0x1 Level(6, Enemy, 1)     [13,8] 0b 0x0 0x0 0x0 [Longbow,] BossAI
UNIT BadGuy Freelancer 0x1 Level(5, Enemy, 1)     [12,11] 0b 0x0 0x0 0x0 [SteelLance,] AttackInRangeAI
UNIT Hiems Shaman 0x1 Level(5, Enemy, 1)     [11,15] 0b 0x0 0x0 0x0 [Flux] AttackInRangeAI
UNIT Hiems Thaumaturge 0x1 Level(6, Enemy, 1)     [12,18] 0b 0x0 0x0 0x0 [Fire] StallOneTurnAI
UNIT Hiems Shaman 0x1 Level(6, Enemy, 1)     [14,10] 0b 0x0 0x0 0x0 [Flux,] BossAI
UNIT BadGuyStealable AxeCav 0x1 Level(5, Enemy, 1)     [14,12] 0b 0x0 0x0 0x0 [IronAxe,Vulnerary] TwoActionAI
UNIT Hiems Thaumaturge 0x1 Level(5, Enemy, 1)     [6,14] 0x0 0x0 0x0 0x0 [Thunder] TwoActionAI
	UNIT

NagitoKomaeda:
UNIT Murray Deserter 0x1 Level(7, Enemy, 0)     [13,15] 0b 0x0 0x0 0x0 [SapphireLance,Javelin,GoddessIcon] BossAI
	UNIT
	
Recruits:
UNIT Noel Thief 0x1 Level(3, Ally, 0)     [14,9] 0b 0x0 0x0 0x0 [IronDagger,RedGem] BossAI
	UNIT
	
SpookyBrigand:
UNIT BadGuyStealable Brigand 0x1 Level(6, Enemy, 1)     [14,14] 0b 0x0 0x0 0x0 [SteelLabrys,GreenGem] BrigandAI
	UNIT
	
SpookyBrigand2:
UNIT BadGuyStealable Brigand 0x1 Level(5, Enemy, 1)     [0,0] 0 0 1 REDA1R8 [SteelAxe,RedGem] BrigandAI
	UNIT
	
TopRightFort:
UNIT Hiems Shaman 0x1 Level(6, Enemy, 1)     [10,3] 0b 0x0 0x0 0x0 [DarkTome2,] ChargeAI
UNIT BadGuy Mercenary 0x1 Level(5, Enemy, 1)     [10,3] 0 0 1 REDA11R3 [SteelBlade,] ChargeAI
	UNIT
	
TopRightFort2:
UNIT BadGuy Deserter 0x1 Level(5, Enemy, 1)     [2,1] 0b 0x0 0x0 0x0 [Axereaver,] StallOneTurnAI
	UNIT

LeftRocks:
UNIT Hiems Shaman 0x1 Level(5, Enemy, 1)     [5,19] 0b 0x0 0x0 0x0 [Flux,] ChargeAI
UNIT BadGuy Hunter 0x1 Level(5, Enemy, 1)     [6,19] 0b 0x0 0x0 0x0 [IronBow,] StallOneTurnAI
	UNIT
	
TopRightForest:
UNIT BadGuy PegRider 0x1 Level(5, Enemy, 1)     [13,0] 0b 0x0 0x0 0x0 [SteelLance] ChargeAI
UNIT BadGuy SCav 0x1 Level(5, Enemy, 1)     [14,0] 0b 0x0 0x0 0x0 [Sagitar,SteelSword] ChargeAI
	UNIT

BottomRightForest:
UNIT Hiems Thaumaturge 0x1 Level(5, Enemy, 1)     [13,19] 0b 0x0 0x0 0x0 [Fire] StallOneTurnAI
	UNIT
	
TopLeftFort:
UNIT BadGuy PegRider 0x1 Level(6, Enemy, 1)     [0,0] 0b 0x0 0x0 0x0 [SteelLance] ChargeAI
UNIT BadGuy PegRider 0x1 Level(5, Enemy, 1)     [0,1] 0b 0x0 0x0 0x0 [Horseslayer] ChargeAI
UNIT BadGuy PegRider 0x1 Level(5, Enemy, 1)     [0,2] 0b 0x0 0x0 0x0 [Axereaver,Javelin] ChargeAI
	UNIT

Traps1:
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
CHECK_ALIVE Irene
BEQ 1 0x0 0xC
Music(0x4C)
SetBackground(0x38)
Text(Ch5Opening1)
LABEL 1
FADI 60
CLEAN
FADU 60
STAL 5
Music(0x2E)
SetBackground(0x34)
Text(Ch5OpeningEnemyMoment)
ClearBackground
STAL 10
CHECK_ALIVE Atheya
BEQ 2 0x0 0xC
CHECK_ALIVE Mocha
BEQ 2 0x0 0xC
Music(0x48)
SetBackground(0x33)
TEXTSTART
TEXTSHOW Ch5Opening2
TEXTEND
MUSC 0x92
TEXTCONT
TEXTEND
FADI 60
CLEAN
LABEL 2
Music(0x24)
SetBackground(0x39)
FADU 40
STAL 5
DoubleConditionalDialog(Atheya,Mocha,Ch5Opening3,Ch5OpeningAlt3,4,5)
FADI 45
	LOAD1 0x1 EnemyUnits
	LOAD1 0x1 NagitoKomaeda
	ENUN
	GotoPrepScreen
	ENDA

EndingScene:
CHECK_EVENTID 0x8
BEQ 1 0x0 0xC
CHECK_EVENTID 0x9
BEQ 1 0x0 0xC
Music(VictoryBGM)
SetBackground(0x33)
Text(Ch5Ending1)
FADI 60
CLEAN
SetBackground(0x0)
FADU 30
Music(0x27)
Text(Ch5Ending2)
ENUT 0x78
GOTO 2; LABEL 1
Music(0x63)
SetBackground(0x33)
Text(Ch5Ending1Alt)
LABEL 2
MNC2 0x6
NoFade
ENDA
	
RecruitThief:
LOAD1 0x1 Recruits
ENUN
SETVAL 1 Thief_Anim|0x8000000
SETVAL 2 0x00090014
ASMC Fancy_Thief_Animation|1
// would play the thief animation at [0x14, 0x9]
STAL 30
CameraCursor(Noel)
Text(NoelIntro)
CUSE Noel
NoFade
ENDA

RecruitRecruitThief:
MUSC(0x30)
Text(NoelRecruit)
CUSA Noel
NoFade
ENDA

MochaRinaConvo:
	MUSI
CharacterConditionalDialog(Atheya,Ch5MochaRina,Ch5MochaRinaAlt)
	MUNO
	MURE 2
	NoFade
	ENDA
	
AffoUlrichCh5Convo:
MUSI
Text(AffoUlrichCh5Talk)
MUNO
NoFade
ENDA

EisDoppioConvo:
MUSI
Text(EisDoppioTalk)
MUNO
NoFade
ENDA
	
ReinforcementsWarning:
Highlight(0x4C)
Text(Ch5ReinWarning)
NoFade
ENDA

KillVillageBrigand:
SpawnReinforcements(SpookyBrigand)
NoFade
ENDA

KillVillageBrigand2:
SpawnReinforcements(SpookyBrigand2)
NoFade
ENDA

LeftRocksRein:
SpawnReinforcements(LeftRocks)
NoFade
ENDA

BottomRightRein:
SpawnReinforcements(BottomRightForest)
NoFade
ENDA

DoubleDeath:
CamOn
SpawnReinforcements(TopRightFort)
STAL 20
SpawnReinforcements(BottomRightForest)
NoFade
ENDA

TopLeftPegasi:
SpawnReinforcements(TopLeftFort)
STAL 20
SpawnReinforcements(TopRightFort2)
NoFade
ENDA

TopRightRein:
SpawnReinforcements(TopRightForest)
NoFade
ENDA

JavelinVillage:
VillageEventItem(JavelinHouseText, SnowVillageBG, Javelin)
ENDA

SBookVillage:
VillageEventItem(SBookHouseText, SnowHouseBG, SecretBook)
ENDA

House1:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
HouseExp(0xA,HouseBG1,Placeholder1)
MUNO // increases music volume
NoFade
ENDA

House2:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
HouseExp(0xB,HouseBG2,Placeholder2)
MUNO // increases music volume
NoFade
ENDA

ShopAwesome:
SHLI SteelSword SteelLance SteelAxe SteelBow Thunder Elixir PureWater
ALIGN 4
