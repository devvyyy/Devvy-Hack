
////////////////////////////////////////////////////
//                                                //
// Disassembled with Nintenlord's Event Assembler //
// Version: 11.0.0.0                              //
// Game: FE8                                      //
// File: FE8_clean.gba                            //
// Offset: $9EC66C                                //
//                                                //
////////////////////////////////////////////////////

EventPointerTable(0x7, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TURN 0x0 BeegAIChange [2,0] 0x0
TURN 0x0 RunawayThieves [2,0] 0x80
TURN 0x0 OMGMyHero [3,0] 0x0
TURN 0x0 Thieves1 [4,0] 0x0
TURN 0x0 ShamanRein1 [6,0] 0x0
//TURN 0x0 ShamanRein3 [9,0] 0x0
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0x10,AffoBreveConvo, Affo, Breve)
CharacterEventBothWays(0xB,HildrUlrichConvo, Hildr, Ulrich)
CharacterEventBothWays(0xC,AnsgarAffoConvo, Ansgar, Affo)
CharacterEventBothWays(0xD,HildrAffoConvo, Hildr, Affo)
CharacterEventBothWays(0xE,UlrichAffoConvo, Ulrich, Affo)
END_MAIN

AffoBreveConvo:
	MUSI
	Text(AffoBreveTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
HildrUlrichConvo:
	MUSI
	Text(HildrUlrichTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
AnsgarAffoConvo:
	MUSI
	Text(AnsgarAffoTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
UlrichAffoConvo:
	MUSI
	Text(AffoUlrichTalkPrologue)
	MUNO
	MURE 2
	NoFade
	ENDA
	
HildrAffoConvo:
	MUSI
	Text(AffoHildrTalk)
	MUNO
	MURE 2
	NoFade
	ENDA
	
LocationBasedEvents:
Chest(SteelBow,13,6)
Door(11,7)
Door(12,7)
Seize(2,2)
END_MAIN

MiscBasedEvents:
AREA 0xE WallSus [10,5] [14,8]
CauseGameOverIfLordDies
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Units1:
UNIT Affogato Hunter 0x1 Level(1, Ally, 0)     [7,6]  0b 0 1 REDA7R9 [IronBow, IronRifle,Elixir] [0x0,0x0,0x0,0x0]
UNIT Breve SCav 0x1 Level(3, Ally, 0)    [7,6] 0b 0 1 REDA6R8 [IronSword,Sagitar,Vulnerary] [0x0,0x0,0x0,0x0]
UNIT Ansgar LArmor 0x1 Level(4, Ally, 0)   [7,6] 0b 0 1 REDA7R8 [IronLance,Javelin,Vulnerary] [0x0,0x0,0x0,0x0]
UNIT Hildr 0x6 0x1 Level(2, Ally, 0)    [7,6] 0b 0 1 REDA8R8 [IronAxe,HandAxe,Vulnerary] [0x0,0x0,0x0,0x0]
UNIT Ulrich APaladin 0x1 Level(5, Ally, 0)   [7,6] 0b 0 1 REDA7R7 [IronAxe, 0xBF,Elixir] [0x0,0x0,0x0,0x0]
UNIT

EnemyUnits:
UNIT Aeteria Shaman 0x1 Level(1, Enemy, 1)     [8,11] 0b 0x0 0x0 0x0 [Flux,] [0x6,0x3,0x4,0x20]
UNIT Aeteria Shaman 0x1 Level(1, Enemy, 1)     [5,1] 0b 0x0 0x0 0x0 [Flux,] AttackInRangeAI
UNIT Aeteria Shaman 0x1 Level(1, Enemy, 1)     [1,10] 0b 0x0 0x0 0x0 [Flux,] [0x6,0x3,0x4,0x20]
UNIT AeteriaStealable Shaman 0x1 Level(1, Enemy, 1)      [12,9] 0b 0x0 0x0 0x0 [Flux,Mine] [0x6,0x3,0x4,0x20]
UNIT Aeteria 0x58 0x1 Level(1, Enemy, 1)      [4,5] 0b 0x0 0x0 0x0 [Flux,] TwoActionAI
UNIT AeteriaStealable Shaman 0x1 Level(1, Enemy, 0)      [13,2] 0b 0x0 0x0 0x0 [Flux,Speedwing] BossAI
UNIT Marcellus 0x58 0x1 Level(5, Enemy, 0)      [2,2] 0b 0x0 0x0 0x0 [DarkTome2,] [0,3,4,32]
UNIT Aeteria Fighter 0x1 Level(1, Enemy, 1)     [5,10] 0b 0x0 0x0 0x0 [IronAxe,] [0x6,0x3,0x4,0x20]
UNIT Aeteria Mercenary 0x1 Level(1, Enemy, 1)     [1,4] 0b 0x0 0x0 0x0 [IronSword,] AttackInRangeAI
UNIT Aeteria Mercenary 0x1 Level(1, Enemy, 1)     [11,0] 0b 0x0 0x0 0x0 [IronSword,] TwoActionAI
UNIT Aeteria Soldier 0x1 Level(1, Enemy, 1)     [12,5] 0b 0x0 0x0 0x0 [IronLance,] AttackInRangeAI
UNIT Aeteria Soldier 0x1 Level(1, Enemy, 1)     [2,7] DropItem 0x0 0x0 0x0 [IronLance,Vulnerary] BossAI
UNIT AeteriaStealable Fighter 0x1 Level(1, Enemy, 1)     [6,3] 0b 0x0 0x0 0x0 [IronAxe,RedGem] AttackInRangeAI
UNIT

ThievesGoBrr:
UNIT 0x64 Thief 0x0 Level(1, Enemy, 1)            [7,6] DropItem 0x0 0x0 0x0 [IronDagger, DevilAxe,] [6,12,4,0]
UNIT

AkatsukiPrologue:
UNIT Akatsuki Trickster 0x1 Level(5, Enemy, 0)     [0,3]  0 0 1 REDA2R3 [SteelBlade,SteelBlade,SteelBlade,SteelBlade,] [0,3,4,32]
UNIT

ThiefRein1:
UNIT 0x64 Thief 0x0 Level(1, Enemy, 1)            [14,5] 0b 0x0 0x0 0x0 [IronDagger, ChestKey,] LootingThiefAI
UNIT

Reinforcements1:
UNIT Aeteria Shaman 0x1 Level(2, Enemy, 1)     [7,6] 0 0 1 REDA7R7 [DarkTome1,] [0,0,4,0]
UNIT Aeteria Shaman 0x1 Level(1, Enemy, 1)     [7,6] 0 0 1 REDA8R6 [DarkTome2,] [0,0,4,0]
UNIT

//Reinforcements3:
//UNIT Aeteria Occultist 0x1 Level(1, Enemy, 1)     [0,2] 0 0 1 REDA1R2 [DarkTome1,] [0,0,4,0]
//UNIT Aeteria Shaman 0x1 Level(1, Enemy, 1)     [0,2] 0 0 1 REDA0R3 [DarkTome2,] [0,0,4,0]
//UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
STAL 20
Music(0x93)
STAL 5
SetBackground(0x32)
STAL 250
CenterTextbox(AmongUsEdgyIntro)
STAL 100
FADI 60
CLEAN
SetBackground(0x35)
MUSC 0x48
SetBackground(0x25)
STAL 10
BROWNBOXTEXT WreyvineText [8,8]
STAL 15
TEXTSTART
TEXTSHOW Prologue1
TEXTEND
MUSC 0xBB
TEXTCONT
TEXTEND
REMA
FADI 16
CLEAN
MUSC 0x4D
FADU 30
LOAD1 0x0 Units1
ENUN
STAL 40
SetBackground(0x3B)
MUSC 0x4E
TEXTSTART
TEXTSHOW Prologue2
TEXTEND
ClearBackground
Music(0x92)
LOAD1 0x1 EnemyUnits
ENUN
STAL 5
SetBackground(0x3C)
TEXTSTART
TEXTSHOW Prologue3
TEXTEND
MUSC 0x4D
TEXTCONT
TEXTEND
REMA
FADI 20
CLEAN
FADU 20
STAL 10
CameraCursor(Affogato)
CameraCursor(Ulrich)
Text(Prologue4)
STAL 5
NoFade
ENDA

EndingScene: //why did i do this to myself
Music(0x32)
SetBackground(0x3C)
CHECK_ALIVE Breve //if alive continue, if dead go to LABEL 0x1
BEQ 1 0xC 0x0
CHECK_ALIVE Hildr
BEQ 2 0x0 0xC
CHECK_ALIVE Ansgar
BEQ 3 0x0 0xC
Text(EndingSceneHildrAnsgarPrologue) //everyone's alive
GOTO 0x420 //end

LABEL 1 //breve dead
CHECK_ALIVE Ansgar //if alive continue, if dead go to LABEL 0x5
BEQ 5 0x0 0xC
CHECK_ALIVE Hildr //if alive plays hildr dialogue, if dead everyone is dead
BEQ 6 0x0 0xC //everyone is dead
Text(EndingSceneAnsgarHildrPrologue)
GOTO 0x420

LABEL 5 //breve dead/ansgar dead
CHECK_ALIVE Hildr //if alive plays hildr dialogue, if dead everyone is dead
BEQ 0x776 0x0 0xC //everyone is dead
Text(EndingSceneHildrPrologue)
GOTO 0x420

LABEL 6 //breve dead/ansgar alive/hildr dead
Text(EndingSceneAnsgarPrologue)
GOTO 0x420

LABEL 2 //hildr dead
CHECK_ALIVE Breve
BEQ 7 0x0 0xC
CHECK_ALIVE Ansgar
BEQ 8 0x0 0xC //everyone is dead
Text(EndingSceneBreveAnsgarPrologue)
GOTO 0x420

LABEL 7 //hildr dead/breve dead
CHECK_ALIVE Ansgar //if alive plays ansgar dialogue, if dead everyone is dead
BEQ 0x776 0x0 0xC //everyone is dead
Text(EndingSceneAnsgarPrologue)
GOTO 0x420

LABEL 8 //hildr dead/ansgar dead/breve alive
Text(EndingSceneBrevePrologue)
GOTO 0x420

LABEL 3 //ansgar dead
CHECK_ALIVE Breve
BEQ 9 0x0 0xC
CHECK_ALIVE Hildr
BEQ 10 0x0 0xC //everyone is dead
Text(EndingSceneBreveHildrPrologue)
GOTO 0x420

LABEL 9 //ansgar dead/breve dead
CHECK_ALIVE Hildr //if alive plays hildr dialogue, if dead everyone is dead
BEQ 0x776 0x0 0xC //everyone is dead
Text(EndingSceneHildrPrologue)
GOTO 0x420

LABEL 10 //ansgar dead/hildr dead/breve alive
Text(EndingSceneBrevePrologue)
GOTO 0x420

LABEL 0x776 //bruh moment
Music(0x4D)
Text(HowDoYouEvenGetThisEnding) //everyone's dead
STAL 30
GiveItem(0xEE,Affogato)

LABEL 0x420 //moves to next cutscene
STAL 20
Music(0xA9)
SetBackground(0x2D)
STAL 5
BROWNBOXTEXT AeteriaCapitalText [8,8]
STAL 15
Text(PrologueAeteriaEnding)
FADI 60
STAL 10
MNC2 0x1
NoFade
ENDA

RunawayThieves:
CamOn
SpawnReinforcements(ThievesGoBrr)
NoFade
ENDA

OMGMyHero:
CAM1 [2,5]
LOAD1 0x1 AkatsukiPrologue
MUSC 0x107
Text(DoorTrick)
MOVE 0x0 Akatsuki [0,6]
ENUN
DISA_IF Akatsuki
NoFade
ENDA

ShamanRein1:
CamOn
SpawnReinforcements(Reinforcements1)
NoFade
ENDA

//ShamanRein3:
//CamOn
//SpawnReinforcements(Reinforcements3)
//NoFade
//ENDA

Thieves1:
SpawnReinforcements(ThiefRein1)
NoFade
ENDA

BeegAIChange:
CAM1 Marcellus
CUMO Marcellus
STAL 60
CURE
Text(OhNoHeNotices)
SVAL 0x1 0x03030000 // sets ai to [0x3,0x3,0x0,0x0]
CHAI [8,11] // changes the ai of this coordinate
SVAL 0x1 0x00120400 // sets ai to [0x0,0x12,0x4,0x0]
CHAI [1,10] // changes the ai of this coordinate
SVAL 0x1 0x00030000 // sets ai to [0x0,0x3,0x0,0x0]
CHAI [5,10] // changes the ai of this coordinate
SVAL 0x1 0x00030000 // sets ai to [0x0,0x3,0x0,0x0]
CHAI [12,9] // changes the ai of this coordinate
SVAL 0x1 0x00060400 // sets ai to [0x0,0x6,0x4,0x0]
CHAI [4,5] // changes the ai of this coordinate
NoFade
ENDA

WallSus:
CAM1 [12,3]
CharacterConditionalDialog(Ansgar,WhenTheWallIsSus2,WhenTheWallIsSus)
NoFade
ENDA