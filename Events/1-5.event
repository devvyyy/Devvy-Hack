EventPointerTable(Ch5xEvent, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
TURN 0x0 KillVillageBrigand [2,0] 0x0
TurnEventEnemy(0,RecruitThief,3)
TURN 0x0 ReinforcementsWarning [3,0] 0x0
TURN 0x0 LeftRocksRein [4,0] 0x0
TURN 0x0 BottomRightRein [5,0] 0x0
TURN 0x0 TopRightRein [6,0] 0x0
TURN 0x0 DoubleDeath [7,0] 0x0
TURN 0x0 TopLeftPegasi [9,0] 0x0
	END_MAIN

ALIGN 32
CharacterBasedEvents:
CharacterEventBothWays(0xC,AffoUlrichConvoCh5, Affogato, Ulrich) //add this later final talk to unlock gaiden
//CharacterEvent(0x0,YoryTurretCheck,Yory,0x3F)
//CharacterEventBothWays(0xC, MochaRinaConvo, Mocha, Rina)
//CharacterEventBothWays(0xD, SeqUlrichConvo, Sequin, Ulrich)
//CharacterEventBothWays(0xE, EisDoppioConvo, Doppio, Eiskaffee)
CharacterEventBothWays(0x20, RecruitRecruitThief, 0x0, Noel)
	END_MAIN

MiscBasedEvents:
	DefeatBoss(EndingScene)
	CauseGameOverIfLordDies
	AREA 0x10 VillageCheck1 [13,6] [13,6]
	AREA 0x11 VillageCheck2 [1,14] [1,14]
	END_MAIN

LocationBasedEvents:
Village(0x8,JavelinVillage,13,6)
Village(0x9,SBookVillage,1,14)
House(0x0,House1,6,17)
House(0x0,House2,13,16)
Vendor(ShopAwesome,6,12)
	END_MAIN
	
AffoUlrichConvoCh5:
	MUSI
	Text(Ch5AffoUlrichTalk) //uhhh they talk about insert past here/remembered shit idk bro!!1
	MUNO
	MURE 2
	NoFade
	ENDA

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
LoadUnit(0,1)
LoadUnit(1,1)
LoadUnit(2,1)
LoadUnit(2,2)
LoadUnit(0,2)
LoadUnit(1,2)
LoadUnit(13,1)
LoadUnit(14,1)
LoadUnit(13,2)
LoadUnit(14,2)
LoadUnit(12,1)
LoadUnit(12,2)
	UNIT

EnemyUnits:
UNIT BadGuy Shaman 0x1 Level(5, Enemy, 1)     [6,2] 0b 0x0 0x0 0x0 [Flux,] AttackInRangeAI
UNIT BadGuy LArmor 0x1 Level(6, Enemy, 1)     [11,5] 0b 0x0 0x0 0x0 [IronGlaive,ShortSpear] BossAI
UNIT BadGuy Shaman 0x1 Level(5, Enemy, 1)     [7,7] 0b 0x0 0x0 0x0 [Flux,] ChargeAI
UNIT BadGuy Mercenary 0x1 Level(5, Enemy, 1)     [5,4] 0b 0x0 0x0 0x0 [IronBlade,] AttackInRangeAI
UNIT BadGuy Freelancer 0x1 Level(6, Enemy, 1)     [6,10] 0b 0x0 0x0 0x0 [IronLance] TwoActionAI
UNIT BadGuy AxeCav 0x1 Level(5, Enemy, 1)     [0,11] 0b 0x0 0x0 0x0 [IronAxe] TwoActionAI
UNIT BadGuy LanceCav 0x1 Level(5, Enemy, 1)     [3,15] 0b 0x0 0x0 0x0 [HeavySpear] StallOneTurnAI
UNIT BadGuyStealable SArmor 0x1 Level(5, Enemy, 1)     [3,18] 0b 0x0 0x0 0x0 [SteelBlade,Elixir] TwoActionAI
UNIT BadGuyStealable Hunter 0x1 Level(5, Enemy, 1)     [4,12] 0b 0x0 0x0 0x0 [Longbow,BlueGem] BossAI
UNIT BadGuy AxeArmor 0x1 Level(5, Enemy, 1)     [9,12] 0b 0x0 0x0 0x0 [Swordreaver,HandAxe] BossAI
UNIT BadGuy Myrm 0x1 Level(5, Enemy, 1)     [8,15] 0b 0x0 0x0 0x0 [Zanbato] AttackInRangeAI
UNIT BadGuy Shaman 0x1 Level(6, Enemy, 1)     [6,18] 0b 0x0 0x0 0x0 [DarkTome2] AttackInRangeAI
UNIT BadGuy SwordCav 0x1 Level(6, Enemy, 1)     [14,19] 0b 0x0 0x0 0x0 [IronSword,Vulnerary] ChargeAI
UNIT BadGuy Mage 0x1 Level(5, Enemy, 1)     [9,4] 0b 0x0 0x0 0x0 [Fire] TwoActionAI
UNIT BadGuy Mercenary 0x1 Level(5, Enemy, 1)     [10,9] 0b 0x0 0x0 0x0 [IronSword,] AttackInRangeAI
UNIT BadGuy Hunter 0x1 Level(5, Enemy, 1)     [0,19] 0b 0x0 0x0 0x0 [IronBow] AttackInRangeAI
UNIT BadGuy Mage 0x1 Level(6, Enemy, 1)     [4,8] DropItem 0x0 0x0 0x0 [Thunder,] StallOneTurnAI
UNIT BadGuy Hunter 0x1 Level(6, Enemy, 1)     [13,8] 0b 0x0 0x0 0x0 [Longbow,] BossAI
UNIT BadGuy Freelancer 0x1 Level(5, Enemy, 1)     [12,11] 0b 0x0 0x0 0x0 [SteelLance,] AttackInRangeAI
UNIT BadGuy Shaman 0x1 Level(5, Enemy, 1)     [11,15] 0b 0x0 0x0 0x0 [Flux] AttackInRangeAI
UNIT BadGuy Thaumaturge 0x1 Level(6, Enemy, 1)     [12,18] 0b 0x0 0x0 0x0 [Fire] StallOneTurnAI
UNIT BadGuy Shaman 0x1 Level(6, Enemy, 1)     [14,10] 0b 0x0 0x0 0x0 [Flux,] BossAI
UNIT BadGuyStealable AxeCav 0x1 Level(5, Enemy, 1)     [14,12] 0b 0x0 0x0 0x0 [IronAxe,Vulnerary] TwoActionAI
UNIT BadGuy Thaumaturge 0x1 Level(5, Enemy, 1)     [6,14] 0x0 0x0 0x0 0x0 [Thunder] TwoActionAI
	UNIT
	
NewNew:
UNIT Atheya WyvernRider_F 0x1 Level(5, Ally, 0)     [6,13] 0x0 0x0 0x0 0x0 [IronLabrys,Halberd,Elixir,] AttackInRangeAI
UNIT

LunaticUnits:
UNIT Atheya WyvernRider_F 0x1 Level(5, Ally, 1)     [6,13] 0x0 0x0 0x0 0x0 [KillerAxe,Halberd,Elixir,Speedwing] AttackInRangeAI
UNIT

NagitoKomaeda:
UNIT Murray Deserter 0x1 Level(7, Enemy, 0)     [13,15] 0b 0x0 0x0 0x0 [SapphireLance,ShortSpear,GoddessIcon] BossAI
	UNIT
	
Recruits:
UNIT Noel Fighter 0x1 Level(3, Ally, 0)     [14,9] 0b 0x0 0x0 0x0 [0x22,0x2C,RedGem] BossAI
	UNIT
	
SpookyBrigand:
UNIT TorchBrigand Brigand 0x1 Level(6, Enemy, 1)     [14,14] 0b 0x0 0x0 0x0 [SteelLabrys,GreenGem] BrigandAI
	UNIT
	
SpookyBrigand2:
UNIT TorchBrigand Raider 0x1 Level(5, Enemy, 1)     [0,8] 0 0 1 REDA1R8 [SteelSword,0xa3] BrigandAI
	UNIT
	
TopRightFort:
UNIT BadGuy Shaman 0x1 Level(6, Enemy, 1)     [10,3] 0b 0x0 0x0 0x0 [DarkTome2,] ChargeAI
UNIT BadGuy Mercenary 0x1 Level(5, Enemy, 1)     [10,3] 0 0 1 REDA11R3 [SteelBlade,Armorslayer] ChargeAI
	UNIT
	
TopRightFort2:
UNIT BadGuy Deserter 0x1 Level(5, Enemy, 1)     [2,1] 0b 0x0 0x0 0x0 [Axereaver,] ChargeAI
UNIT BadGuy PegRider 0x1 Level(5, Enemy, 1)     [14,2] 0b 0x0 0x0 0x0 [SteelLance,] StallOneTurnAI
	UNIT

LeftRocks:
UNIT BadGuy Shaman 0x1 Level(5, Enemy, 1)     [5,19] 0b 0x0 0x0 0x0 [Flux,] ChargeAI
UNIT BadGuy Hunter 0x1 Level(5, Enemy, 1)     [6,19] 0b 0x0 0x0 0x0 [IronBow,] StallOneTurnAI
	UNIT
	
TopRightForest:
UNIT BadGuy PegRider 0x1 Level(5, Enemy, 1)     [13,0] 0b 0x0 0x0 0x0 [SteelLance] ChargeAI
UNIT BadGuy SCav 0x1 Level(5, Enemy, 1)     [14,0] 0b 0x0 0x0 0x0 [Sagitar,SteelSword] ChargeAI
	UNIT

BottomRightForest:
UNIT BadGuy Thaumaturge 0x1 Level(5, Enemy, 1)     [13,19] 0b 0x0 0x0 0x0 [Fire] StallOneTurnAI
	UNIT
	
TopLeftFort:
UNIT BadGuy PegRider 0x1 Level(6, Enemy, 1)     [0,0] 0b 0x0 0x0 0x0 [SteelLance] ChargeAI
UNIT BadGuy PegRider 0x1 Level(5, Enemy, 1)     [0,1] 0b 0x0 0x0 0x0 [Horseslayer] ChargeAI
UNIT BadGuy PegRider 0x1 Level(5, Enemy, 1)     [0,2] 0b 0x0 0x0 0x0 [Axereaver,Javelin] ChargeAI
	UNIT
	
CutsceneUnits1:
UNIT Ulrich LPaladin 0x1 Level(1, Ally, 1)     [3,0] 0b 0x0 0x0 0x0 [SteelBlade,SteelBlade,SteelBlade,SteelBlade] [0x0,0x0,0x0,0x0]
	UNIT
	
CutsceneUnits2:
UNIT Affogato Scout 0x1 Level(1, Ally, 1)     [1,3] 0 0 1 REDA4R1 [SteelBlade,SteelBlade,SteelBlade,SteelBlade] [0x0,0x0,0x0,0x0]
UNIT Brudet WyvernRider_F 0x1 Level(5, Ally, 1)     [0,3] 0 0 1 REDA3R1 [IronLabrys,Halberd,Elixir,] [0x0,0x0,0x0,0x0]
	UNIT
	
CutsceneUnits3:
UNIT Ulrich LPaladin 0x1 Level(1, Ally, 1)     [13,17] 0b 0x0 0x0 0x0 [SteelBlade,SteelBlade,SteelBlade,SteelBlade] [0x0,0x0,0x0,0x0]
UNIT Affogato Scout 0x1 Level(1, Ally, 1)     [14,16] 0b 0x0 0x0 0x0 [SteelBlade,SteelBlade,SteelBlade,SteelBlade] [0x0,0x0,0x0,0x0]
	UNIT

Traps1:
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
//talk only exists if previous talks were fulfilled
CHECK_EVENTID 0x79 //affo talk 1
BNE 1 0x0 0xC
CHECK_EVENTID 0x7A //affo talk 2
BNE 1 0x0 0xC
GOTO 2

LABEL 1
SetFlag(0xC) //prevents talk if previous 2 werent done

LABEL 2

Music(0x4C)
SetBackground(0x38)
Text(Ch5Opening1)
FADI 60
CLEAN
FADU 60
STAL 5
	LOAD2 0x1 CutsceneUnits1
	LOAD2 0x1 CutsceneUnits2
STAL 15
Music(0x24)
SetBackground(0x33)
TEXTSTART
TEXTSHOW Ch5Opening2
TEXTEND
FADI 60
CLEAN
STAL 5
FADI 45
	LOAD1 0x1 EnemyUnits
	LOAD1 0x1 NagitoKomaeda
HardUnits(LunaticUnits,NewNew)
	ENUN
	GotoPrepScreen
	ENDA

EndingScene:
MuteMusic
FADI 60
DoAllThree
SetFlag(0x84)
CamOn
LOAD2 0x1 CutsceneUnits3
CamOff
FADU 60
STAL 10
CameraCursor(Affogato)
CameraCursor(Ulrich)
STAL 10
MUSCSLOW VictoryBGM
SetBackground(0x33)
STAL 5
	CHECK_ALIVE Ulrich
	BEQ 0x1 0xC 0x0
CHECK_EVENTID 0x79 //skips to normal ending if 1 wasnt completed
BEQ 1 0x0 0xC
CHECK_EVENTID 0x7A //skips to normal ending if 2 wasnt completed
BEQ 1 0x0 0xC
CHECK_EVENTID 0xC //affo talk 3; only possible if 1 and 2 were completed
BEQ 1 0x0 0xC //if flag isnt set skip to normal ending

TEXTSTART
TEXTSHOW Ch5Ending1
TEXTEND
MUSC 0x115
TEXTSHOW Ch5EndingGaiden
TEXTCONT
TEXTEND
REMA

SetFlag(0x7B) //GAIDEN UNLOCKED EID FLAG
UnsetFlag(0x79)
UnsetFlag(0x7A)

CenterTextbox(Ch5GaidenUnlock)
GiveItem(0xEE,Ulrich)
GiveItem(0x89,Ulrich)
GOTO 2

LABEL 1

Text(Ch5Ending1)

LABEL 2
FADI 60
CLEAN
MNC2 0xF
NoFade
ENDA

Turn1Moment:
CameraCursor(0x4c)
Music(0x2E)
SetBackground(0x34)
STAL 5
BROWNBOXTEXT WreyvianOutskirtsText [8,8] // box in the top right that's pretty cool for small text
STAL 15
Text(Ch5OpeningEnemyMoment)
ClearBackground
NoFade
ENDA
	
RecruitThief:
LOAD1 0x1 Recruits
ENUN
CameraCursor(Noel)
Text(NoelIntro)
CUSE Noel
NoFade
ENDA

RecruitRecruitThief:
MUSC(0x30)
Text(NoelRecruit)
MURE 0x2
CUSA Noel

STAL 20
	CHECK_EVENTID 0x12C
	BNE 0x1 0x0 0xC
	CenterTextbox(RingDisclaimer)
	LABEL 0x1

UnsetFlag(0x12C)
NoFade
ENDA

MochaRinaConvo:
	MUSI
CharacterConditionalDialog(Atheya,Ch5MochaRina,Ch5MochaRinaAlt)
	MUNO
	MURE 2
	NoFade
	ENDA
	
SeqUlrichConvo:
MUSI
Text(SequinUlrichConvo)
MUNO
NoFade
ENDA

EisDoppioConvo:
MUSI
Text(EisDoppioTalk)
MUNO
NoFade
ENDA
	
ReinforcementsWarning:
CameraCursor(Kevron)
Text(Ch5ReinWarning)
NoFade
ENDA

KillVillageBrigand:
SpawnReinforcements(SpookyBrigand)
STAL 10
SpawnReinforcements(SpookyBrigand2)
NoFade
ENDA

LeftRocksRein:
SpawnReinforcements(LeftRocks)
NoFade
ENDA

BottomRightRein:
SpawnReinforcements(BottomRightForest)
NoFade
ENDA

DoubleDeath:
CamOn
SpawnReinforcements(TopRightFort)
STAL 20
SpawnReinforcements(BottomRightForest)
NoFade
ENDA

TopLeftPegasi:
SpawnReinforcements(TopLeftFort)
STAL 20
SpawnReinforcements(TopRightFort2)
NoFade
ENDA

TopRightRein:
SpawnReinforcements(TopRightForest)
NoFade
ENDA

JavelinVillage:
VillageEventItem(JavelinHouseText, SnowVillageBG, ShortSpear)
ENDA

SBookVillage:
VillageEventItem(SBookHouseText, SnowHouseBG, SecretBook)
ENDA

House1:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
HouseExp(0xA,HouseBG1,Ch5HouseLeft)
MUNO // increases music volume
NoFade
ENDA

House2:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
HouseExp(0xB,HouseBG2,Ch5HouseRight)
MUNO // increases music volume
NoFade
ENDA

ShopAwesome:
SHLI SteelSword SteelLance SteelAxe SteelBow Mend Elixir PureWater
ALIGN 4

VillageCheck1:
CHECK_ACTIVE
SVAL 1 0x88
BEQ 0x1 0xC 0x1
ENUF 0x10
LABEL 0x1
NoFade
ENDA

VillageCheck2:
CHECK_ACTIVE
SVAL 1 0x88
BEQ 0x1 0xC 0x1
ENUF 0x11
LABEL 0x1
NoFade
ENDA

